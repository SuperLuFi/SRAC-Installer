C=======================================================================
      SUBROUTINE PONDEL( SUBNAM, IPDS, MEMBER, IDEL, IPRN, IOUT )
C=======================================================================
C  SEARCH & DELETE A MEMBER FROM VPDS AND PDS
C  EVEN IF THE MEMBER IS NOT FOUND IN VPDS OR PDS, THAT IS TREATED AS 
C  NORMAL END.
C                                      BY K.OKUMURA(JAERI),OCT/1999
C
C  I   SUBNAM : NAME OF SUBROUTINE WHICH CALLS THIS SYSTEM
C  I   IPDS   : DIRECTORY NUMBER
C  I   MEMBER : NAME OF MEMBER(A8)
C  I   IDEL   : =0  USUAL MODE (DELETE FROM VPDS & PDS)
C               >0  SPECIAL MODE (DELETE FROM ONLY VPDS)
C  I   IPRN   : PRINT CONTROL FOR MEMBER ACCESS INFORMATION
C  I   IOUT   : OUTPUT DEVICE NUMBER FOR ERROR OR CAUTION MESSAGES
C 
C  NOTE : ATTRIBUTION(MATTR) OF A GHOST MEMBER IS ZERO(0), BUT
C         THE GHOST MEMBER HAS NON-ZERO MLENG & MADRS
C
C--------1---------2---------3---------4---------5---------6---------7--
C
      INCLUDE 'INCPDS'
      COMMON  /USPDSC/ PDSDIR(MXPDS), MOACS(MXPDS), MOSTY(MXPDS)
      COMMON  /VPDSI1/ LMEMBR, NMEMBR, LASTAD, LOCUPY, ISTATS, NFAIL
      COMMON  /VPDSI2/ MLENG(MXMBR), MATTR(MXMBR), MADRS(MXMBR)
      COMMON  /VPDSC1/ MNAME(MXMBR), MDACS(MXPDS)
C
      CHARACTER*4    MOACS, MOSTY, MDACS
      CHARACTER*8    SUBNAM
      CHARACTER*8    MEMBER, MNAME
      CHARACTER*120  PDSDIR, DIRNAM
C
C=======================================================================
C
      DIRNAM = PDSDIR(IPDS)
C
C-----SERACH MEMBER IN VPDS AND DELETE IT (SET AS A GHOST MEMBER)
      CALL PONSRC( MEMBER, IPDS, IEXT, LENG, IPOS, ITOP )
      IF (IEXT.EQ.1) THEN
        MNAME(IPOS) = '        '
        MATTR(IPOS) = 0
CDEL    MLENG(IPOS) = 0
CDEL    MADRS(IPOS) = 0
        NMEMBR = NMEMBR - 1
        LOCUPY = LOCUPY - LENG
      ENDIF
C
C-----DELETE MEMBER IN PDS
      IF(IDEL.EQ.0) THEN
        CALL PDSRM(DIRNAM,MEMBER,IRC)
        CALL PDSER(SUBNAM,DIRNAM,MEMBER,IRC,IOUT)
        IF(IPRN.GT.0) THEN
          WRITE(IOUT,6000) '     MEMBER ',MEMBER,' WAS DELETED FROM ',
     &                     DIRNAM
        ENDIF
      ENDIF
C
 6000 FORMAT(A,A,A,/,5X,A)
      RETURN
      END
