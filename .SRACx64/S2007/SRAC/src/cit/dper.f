CDPER --107 ***CITATION*** LINE RELAXATION ON ROWS AND/OR COLUMNS FOR
C                          PERIODIC BOUNDARY CONDITIONS (2-D)/ CF-DNSD
C
      SUBROUTINE DPER(SCAT,P2,DCONB,DCONR,PTSA,NRGN,E1,BET,DEL, IVX,JVX,
     & KVX,IVXP1,JVXP1,IVZ,KVZ,LVX,IOVX,IOVZ)
C
CDEL  INTEGER RGX , MSX , ZNEX , ZDX , WZX
CDEL  PARAMETER ( RGX=100, MSX=211, ZDX=200, ZNEX=1000, WZX=100 )
      INCLUDE  'CITPMINC'
C
      REAL*8 ALP(MSX),Q(MSX),QQ,TT,D,E,F
      REAL*8 P2
      REAL*8 BET(MSX),DEL(MSX),T,TEMP,TMF
      REAL*8 SCAT
      REAL*8 RDEL
C
      COMMON/ALSUB/BLSUB(30),TITL1(18),TITL2(18),IMAX,JMAX,KBMAX,KMAX,
     & LMAX,MMAX, NMAX,IMXP1,JMXP1,KBMXP1,NSETMX,NTO,MM1VX,KM1VX,IOIN,
     & IOUT,IOSIG,IOFLX,IO1,IO2,IO3,IO4,IO7,NER(100), IX(200),INNO(100),
     &  NGC(24),IEDG(24),ITMX(24),TIMX(6), GLIM(6),NDPL(24),IEDP1(24),
     & IEDP2(24),IEDP3(24), DPLH(6),NUAC(24),EPI(6),XMIS(6),NSRH(24),
     & XSRH1(6), XTR1(WZX),XTR2(WZX),NXTR1(WZX),NXTR2(WZX),SPARE(200),
     & IXPUT(200),XPUT(200)
      COMMON/AFLUX/BFLUX(30),KXMN8,NIT,NIIT,NIIIT,JXP1,KSCT1,KSCT2,
     & ISTART,IEP, VRGP1,VRGP2,VRGP3,VRG1,VRG2,VRGK1,VRGK2,XABS,PROD,
     & XLEK,RMX,RMN,XKEF1,XKEF2,XKEF3,EXFC1,EXFC2,EXFC3, NI3,IEXTR,
     & IRECV,VRGABS,LO3,LO4,XLAMDA,EPI1,EPI2, BETTA,SUMXI,IX25,IX28,I,J,
     &  KB,K,ITMAX,ITIME, BAT(MSX),DAL(MSX)
C
      DIMENSION SCAT(JVX,IVX), P2(JVX,IVX,KVX),DCONB(JVX,IVXP1,IOVX),
     & DCONR(JVXP1,IVZ,IOVZ),PTSA(JVX,IVX,IOVX)
      DIMENSION NRGN(JVX,IVX),E1(LVX,KVX)
C
      JVXM1 = JVX - 1
      N = IX(20)
      DO 124 I=1,IVX
      IM1 = I - 1
      IP1 = I + 1
      DO 102 J=1,JVX
      QQ = SCAT(J,I)
      IF (I.LE.1) GO TO 100
      QQ = QQ + P2(J,IM1,K)*DCONB(J,I,N)
  100 IF (I.GE.IVX) GO TO 101
      QQ = QQ + P2(J,IP1,K)*DCONB(J,IP1,N)
  101 Q(J) = QQ
  102 CONTINUE
      D4 = DCONR(2,I,N)
      IF (P2(1,I,K).EQ.0.0) GO TO 103
      L = NRGN(1,I)
      BET(1) = Q(1)/D4
      DEL(1) = D4/(PTSA(1,I,N) + E1(L,K))
      ALP(1) = DCONR(1,I,N)/D4
      GO TO 104
  103 BET(1) = 0.0
      DEL(1) = 0.0
      ALP(1) = 0.0
  104 CONTINUE
      DO 106 J=2,JVX
      IF (P2(J,I,K).EQ.0.0) GO TO 105
      L = NRGN(J,I)
      T = D4*DEL(J-1)
      D4 = DCONR(J+1,I,N)
      BET(J) = (Q(J) + BET(J-1)*T)/D4
      DEL(J) = D4/(PTSA(J,I,N) + E1(L,K) - T)
      ALP(J) = ALP(J-1)*T/D4
      GO TO 106
  105 BET(J) = 0.0
      DEL(J) = 0.0
      ALP(J) = 0.0
  106 CONTINUE
      IF (P2(JVX,I,K).NE.0.0) GO TO 107
      TEMP = 0.0
      TT = 0.0
      GO TO 115
  107 CONTINUE
      F = 1.0
      D = 0.0
      E = 0.0
      DO 108 J=1,JVXM1
      F = F*DEL(J)
      IF (DEL(J).EQ.0.0) GO TO 109
      D = D + BET(J)*F
      E = E + ALP(J)*F
  108 CONTINUE
  109 CONTINUE
      RDEL = 0.0
      IF (DEL(JVXM1).NE.0.0) RDEL = 1.0D0/DEL(JVXM1)
      L = NRGN(JVX,I)
      D1 = DCONR(JVX,I,N)
      D2 = DCONR(JVX+1,I,N)
      D4 = D1 + F*D2
      TEMP = ((Q(JVX ) + D*D2)*RDEL + D4*BET(JVXM1))/ ((PTSA(JVX,I,N) +
     & E1(L,K) - E*D2)*RDEL - D4*(1.0 + ALP(JVXM1)))
      TT = TEMP
      T = P2(JVX,I,K)
      TMF = T + BETTA*(TEMP - T)
      IF (IEP) 110,114,111
  110 P2(JVX,I,K) = TEMP
      GO TO 115
  111 IF (TMF-TEMP) 113,114,112
  112 TMF = DMIN1(TMF,(TEMP+T))
      GO TO 114
  113 TMF = DMAX1(TMF,0.5*TEMP)
  114 CONTINUE
      P2(JVX,I,K) = TMF
  115 DO 123 JJ=2,JVX
      J = JVXP1 - JJ
      T = P2(J,I,K)
      IF (T.NE.0.0) GO TO 116
      TEMP = 0.0
      GO TO 123
  116 CONTINUE
      TEMP = DEL(J)*(TEMP+BET(J) + TT*ALP(J))
      TMF = T + BETTA*(TEMP - T)
      IF (IEP) 117,121,118
  117 P2(J,I,K) = TEMP
      GO TO 123
  118 IF (TMF-TEMP) 120,121,119
  119 TMF = DMIN1(TMF,(TEMP+T))
      GO TO 121
  120 TMF = DMAX1(TMF,0.5*TEMP)
  121 CONTINUE
  122 P2(J,I,K) = TMF
  123 CONTINUE
  124 CONTINUE
      IF (NUAC(20).GT.0) GO TO 148
      DO 147 J=1,JVX
      JM1 = J - 1
      JP1 = J + 1
      DO 128 I=1,IVX
      QQ = SCAT(J,I)
      IF (J.NE.1) GO TO 125
      QQ = QQ + P2(JVX,I,K)*DCONR(J,I,N) + P2(JP1,I,K)*DCONR(JP1,I,N)
      GO TO 127
  125 IF (J.NE.JVX) GO TO 126
      QQ = QQ + P2(JVX-1,I,K)*DCONR(JVX,I,N) + P2(1,I,K)* DCONR(JVX+1,I,
     & N)
      GO TO 127
  126 QQ = QQ + P2(JM1,I,K)*DCONR(J,I,N) + P2(JP1,I,K)*DCONR(JP1,I,N)
  127 Q(I) = QQ
  128 CONTINUE
      D4 = DCONB(J,2,N)
      IF (P2(J,1,K).EQ.0.0) GO TO 129
      L = NRGN(J,1)
      BET(1) = Q(1)/D4
      DEL(1) = D4/(PTSA(J,1,N) + E1(L,K))
      GO TO 130
  129 DEL(1) = 0.0
  130 CONTINUE
      DO 132 I=2,IVX
      IF (P2(J,I,K).EQ.0.0) GO TO 131
      L = NRGN(J,I)
      T = D4*DEL(I-1)
      D4 = DCONB(J,I+1,N)
      BET(I) = (Q(I) + BET(I-1)*T)/D4
      DEL(I) = D4/(PTSA(J,I,N) + E1(L,K) - T)
      GO TO 132
  131 DEL(I) = 0.0
  132 CONTINUE
  133 TEMP = BET(IVX)*DEL(IVX)
      T = P2(J,IVX,K)
      TMF=T+VRGK2*(TEMP-T)
      IF (IEP) 134,138,135
  134 P2(J,IVX,K)=TEMP
      GO TO 139
  135 IF (TMF-TEMP) 137,138,136
  136 TMF = DMIN1(TMF,(TEMP+ T))
      GO TO 138
  137 TMF = DMAX1(TMF,0.5*TEMP)
  138 CONTINUE
      P2(J,IVX,K)=TMF
  139 DO 146 JJ = 2,IVX
      I = IVXP1-JJ
      T = P2(J,I,K)
      TEMP = DEL(I)*(TEMP+BET(I))
      TMF=T+VRGK2*(TEMP-T)
      IF (IEP) 140,144,141
  140 P2(J,I,K)=TEMP
      GO TO 146
  141 IF (TMF-TEMP) 143,144,142
  142 TMF = DMIN1(TMF,(TEMP+ T))
      GO TO 144
  143 TMF = DMAX1(TMF,0.5*TEMP)
  144 CONTINUE
  145 P2 (J,I, K) = TMF
  146 CONTINUE
  147 CONTINUE
  148 CONTINUE
      RETURN
      END
