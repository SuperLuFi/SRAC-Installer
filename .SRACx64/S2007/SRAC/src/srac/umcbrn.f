      SUBROUTINE UMCBRN(MTH,NPMAX,XCOLD,SCOLD,CKCOLD,SHOT,
     +                  UREACT,UNRES1,UNRES2,OVPI,OVPI2,ATOP )
C
C     GIVEN A CROSS SECTION THAT IS DESCRIBED BY A TABLE OF
C     X/S VS. X AND LINEAR-LINEAR INTERPOLATION BETWEEN POINTS
C     THIS ROUTINE WILL EXACTLY DOPPLER BROADENING THE CROSS
C     SECTION AT A PORTION OF THE ENERGIES CORRESPONDING TO THE
C     ORIGINAL CROSS SECTION.
C
C     INPUT PARAMETERS
C     COLD1 =INDEX TO FIRST POINT LOADED IN CORE.
C     COLD2 =INDEX TO LAST POINT LOADED IN CORE.
C     COLD1P=INDEX TO FIRST POINT TO USE IN DOPPLER INTEGRATION
C     COLD2P=INDEX TO LAST POINT TO USE IN DOPPLER INTEGRATION
C            THE PAIRS (COLD1,COLD2) AND (COLD1P,COLD2P) WILL DIFFER
C            ONLY IF THERE IS AN UNRESOLVED RESONANCE PRESENT.
C     HOT1  =INDEX TO FIRST POINT TO BROADEN.
C     HOT2  =INDEX TO LAST POINT TO BROADEN.
C     XCOLD =TABLE OF SPEEDS CORRESPONDING TO SCOLD.
C     SCOLD =TABLE OF CROSS SECTIONS AT INITIAL TEMPERATURE.
C     CKCOLD=SLOPE BETWEEN POINTS IN (XCOLD,SCOLD) TABLE.
C     EHOT  =TABLE OF ENERGIES CORRESPONDING TO SHOT (AND SCOLD).
C     SHOT  =TABLE OF CROSS SECTIONS BROADENED TO TEMPK.
C     ALPHA =DOPPLER WIDTH (11505.3*AWR/TEMPEF)
C
C     THE TABLE OF POINTS (COLD1P,COLD2P) IS USED TO BROADEN THE
C     TABLE OF POINTS (HOT1,HOT2).
C
C     IF THERE ARE 6012 (3 PAGES) OR FEWER DATA POINTS ALL DOPPLER
C     BROADENED CROSS SECTIONS WILL BE CALCULATED IN ONE PASS THROUGH
C     THIS ROUTINE. IF THERE ARE OVER 6012 POINTS THE DATA WILL BE
C     DOPPLER BROADENED A PAGE AT A TIME. AN EXCEPTION IS THAT AT THE
C     BEGINNING OF THE SECTION THE FIRST TWO PAGES WILL BE BROADENED
C     IN ONE PASS AND AT THE END OF THE SECTION THE LAST TWO PAGES WILL
C     BE DOPPLER BROADENED.
C
C     NORMALLY ALL OF THE POINTS LOADED INTO CORE WILL BE USED IN THE
C     DOPPLER INTEGRATION, I.E. COLD1P=COLD1 AND COLD2P=COLD1P. HOWEVER
C     IF THE THREE PAGES OF DATA THAT ARE IN CORE CONTAIN ANY PORTION OF
C     THE UNRESOLVED RESONANCE REGION THE DOPPLER INTEGRATION WILL BE
C     CUT OFF AT THE EDGE OF THE UNRESOLVED REGION AND THE CROSS SECTION
C     WILL BE EXTENDED FROM THAT POINT AS 1/V. IN THIS CASE FOR DATA
C     POINTS AT ENERGIES LESS THAN THE UNRESOLVED RESONANCE REGION
C     COLD2P WILL POINT TO THE LOWER ENERGY LIMIT OF THE UNRESOLVED
C     REGION AND COLD1P WILL POINT TO THE FIRST POINT IN CORE. SIMILARLY
C     FOR POINTS ABOVE THE UPPER ENERGY LIMIT OF THE UNRESOLVED REGION
C     COLD1P WILL POINT TO THE UPPER ENERGY LIMIT OF THE UNRESOLVED
C     REGION AND COLD2P WILL POINT TO THE LAST POINT LOADED IN CORE.
C     WITHIN THE UNRESOLVED ENERGY RANGE THE ORIGINAL DATA POINT WILL
C     JUST BE COPIED WITHOUT DOPPLER BROADENING AND COLD1P AND COLD2P
C     ARE NOT USED.
C
      INTEGER COLD1,COLD2,COLD1P,COLD2P,HOT1,HOT2,HEATME,HEATER,HIHEAT,
     1        UREACT,UNRES1,UNRES2
C***** EXPORT ******
      DOUBLE PRECISION OVPI,OVPI2
C***** EXPORT ******
C
      COMMON/UMCIDX/COLD1,COLD2,COLD1P,COLD2P,HOT1,HOT2,N2IN,
     1              HEATME,HEATER,LOHEAT,HIHEAT
C
      REAL*4    XCOLD(NPMAX),SCOLD(NPMAX),CKCOLD(NPMAX),SHOT(NPMAX)
C
C     CONVERT FROM ENERGY TO SPEED AND DEFINE SLOPES BETWEEN POINTS.
C
C-----DEFINE OFFSET INDICES TO COLD TABLE TABLE (INDICES TO LAST
C-----INTERIOR ENERGY INTERVALS OF COLD CROSS SECTION TABLE).
C
C     BROADEN DATA. SPECIAL TREATMENT FOR LOW ENERGY.
C
      COLD1P = HOT1
      COLD2P = HOT2
C
      DO 10 I = HOT1 , HOT2
      ISTHOT  = I
      IF(XCOLD(I).GT.ATOP) GO TO 11
   10 CONTINUE
      ISTHOT = HOT2 + 1
   11 CONTINUE
C
      IF(UREACT.EQ.0) THEN
                      LOHEAT=COLD1P+1
                      HIHEAT=COLD2P-1
                      IF(ISTHOT.GT.1) THEN
                                      IST   = COLD1P
                                      IEND  = ISTHOT - 1
                CALL  UMCBRL(IST,IEND,MTH,NPMAX,XCOLD,SCOLD,SHOT,
     +                       OVPI,OVPI2,ATOP )
                                      ENDIF
                      IF(HEATME.GT.HOT2) RETURN
                CALL  UMCBRH(COLD2P,MTH,NPMAX,XCOLD,SCOLD,CKCOLD,SHOT,
     +                       OVPI,OVPI2,ATOP )
                      RETURN
                      ENDIF
C
      COLD1P = HOT1
      COLD2P = UNRES1
      LOHEAT = COLD1P+1
      HIHEAT = COLD2P-1
      IPASSL = 0
C
      IF(COLD2P.GT.0) THEN
                      IF(ISTHOT.GT.COLD2P) THEN
                                           IST   = COLD1P
                                           IEND  = COLD2P
                CALL  UMCBRL(IST,IEND,MTH,NPMAX,XCOLD,SCOLD,SHOT,
     +                       OVPI,OVPI2,ATOP )
                                           ELSE
                                           IST   = COLD1P
                                           IEND  = ISTHOT -1
                CALL  UMCBRL(IST,IEND,MTH,NPMAX,XCOLD,SCOLD,SHOT,
     +                       OVPI,OVPI2,ATOP )
                CALL  UMCBRH(COLD2P,MTH,NPMAX,XCOLD,SCOLD,CKCOLD,SHOT,
     +                       OVPI,OVPI2,ATOP )
                                           IPASSL = 1
                                           ENDIF
                      ENDIF
C
      DO 100 HEATME = UNRES1+1 , UNRES2-1
      SHOT(HEATME)  = SCOLD(HEATME)
  100 CONTINUE
C
      IF(HEATME.GE.HOT2) RETURN
C
      COLD1P = UNRES2
      COLD2P = HOT2
      LOHEAT = COLD1P+1
      HIHEAT = COLD2P-1
      HEATME = UNRES2
C
      IF(IPASSL.EQ.0) THEN
                      IST   = COLD1P
                      IEND  = ISTHOT -1
                      CALL  UMCBRL(IST,IEND,MTH,NPMAX,XCOLD,SCOLD,SHOT,
     +                             OVPI,OVPI2,ATOP )
                      IF(HEATME.GT.HOT2) RETURN
                      ENDIF
C
      CALL  UMCBRH(COLD2P,MTH,NPMAX,XCOLD,SCOLD,CKCOLD,SHOT,
     +             OVPI,OVPI2,ATOP )
C
      RETURN
      END
