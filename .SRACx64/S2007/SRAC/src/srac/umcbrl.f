      SUBROUTINE UMCBRL(IST,IEND,MTH,NPMAX,XCOLD,SCOLD,SHOT,
     +                  OVPI,OVPI2,ATOP )
C
C     LOW ENERGY DOPPLER BROADENING ROUTINE. THIS ROUTINE WILL
C     BE USED TO DOPPLER BROADEN ALL CROSS SECTIONS AT ENERGIES
C     WHERE AE/KT IS LESS THAN OR EQUAL TO 16. ANY POINT WITH A
C     HIGHER ENERGY WILL BE PASSED ON TO ROUTINE BROADH.
C
C     FOR AE/KT LESS THAN OR EQUAL TO 16 BOTH EXPONENTIALS IN THE
C     DOPPLER BROADENING KERNEL MUST BE CONSIDERED. FOR HIGHER ENERGIES
C     THE SECOND EXPONENTIAL MAY BE IGNORED, WHICH SIMPLIFIES THE
C     DOPPLER BROADENING.
C
C     THE ROUTINE HAS BEEN DESIGNED WITH NO SUBROUTINE CALLS
C     IN ORDER TO MINIMIZE RUNNING TIME. THE ARITHMETIC
C     STATEMENT FUNCTIONS RATION(A) AND ERFC(R,EXPERF) WILL
C     BE COMPILED AS IN LINE CODING BY VIRTUALLY ANY FORTRAN
C     COMPILER, AND AS SUCH DO NOT REPRESENT FUNCTION CALLS.
C
      INTEGER COLD1,COLD2,COLD1P,COLD2P,HOT1,HOT2,
     1        HEATME,HEATER,HIHEAT
C***** EXPORT ******
      DOUBLE PRECISION A1,A2,A3,A4,A5,Y,A,B,R,BMY,BPY,YMA,YPA,
     1 YY,YYA,DUMMY1,F2B,F2A,Y2,ERFCY2,RBMY,RBPY,RYMA,RYPA,
     2 EXPERF,EXBMY,EXBPY,EXYMA,EXYPA,ZEXP,RATION,ERFC,OVPI,OVPI2,
     3 ERFCBM,ERFCBP,ERFCYM,ERFCYP,EXY2,Z
C***** EXPORT ******
C
      COMMON/UMCIDX/COLD1,COLD2,COLD1P,COLD2P,HOT1,HOT2,N2IN,
     1              HEATME,HEATER,LOHEAT,HIHEAT
C
      REAL*4 XCOLD(NPMAX),SCOLD(NPMAX),SHOT(NPMAX)
C
C     COMPLEMENTARY ERROR FUNCTION DEFINITION.
C
C-----DEFINE CONSTANTS FOR COMPLEMENTARY ERROR FUNCTION
      DATA A1,A2,A3,A4,A5/0.254829592D+00,-0.284496736D+00,
     1 1.421413741D+00,-1.453152027D+00,1.061405429D+00/
C-----DEFINE ARITHMETIC STATEMENT FUNCTION FOR RATIONAL ARGUMENT
C-----OF COMPLEMENTARY ERROR FUNCTION.
      RATION(A)=1./(1.+0.3275911*A)
C-----DEFINE ARITHMETIC STATEMENT FUNCTION FOR COMPLEMENTARY
C-----ERROR FUNCTION.
      ERFC(R,EXPERF)=((((A5*R+A4)*R+A3)*R+A2)*R+A1)*R*EXPERF
C-----DEFINE ARITHMETIC STATEMENT FUNCTION FOR LINEAR INTERPOLATION.
CKSK  TERP(X,X1,X2,Y1,Y2)=((X2-X)*Y1+(X-X1)*Y2)/(X2-X1)
      TERP(X,X1,X2,Y1,Y2W)=((X2-X)*Y1+(X-X1)*Y2W)/(X2-X1)
C-----DEFINE ARITHMETIC STATEMENT FUNCTION FOR SINGLE AND DOUBLE
C-----PRECISION EXPONENTIAL.
      ZEXP(Z)=DEXP(Z)
C
C     SET UP LOOP TO DOPPLER BROADEN CROSS SECTIONS.
C
CDEL  WRITE(6,6010) MTH,IST,IEND,COLD1P,COLD2P
 6010 FORMAT(1H ,' ## MT IST IEND COLD1P COLD2P (UMCBRL) ## ',6I6)
CM    DO 230 HEATME=HOT1,HOT2
      DO 230 HEATME=IST,IEND
C-----INITIALIZE INTEGRAL AND CONSTANTS.
      Y     = XCOLD(HEATME)
C-----DEFINE ALL REQUIRED CONSTANTS FOR POINT.
      Y2    = 2.0*Y
      YY    = Y*Y
      YYA   = 0.5 + YY
C-----INITIALIZE INTEGRALS.
      DUMMY1= 0.0
      IF(Y2.LE.ATOP) GO TO 50
      ERFCY2= 0.0
      F2B   = YYA + OVPI2*Y
      GO TO 60
   50 R     = RATION(Y2)
      EXY2  = ZEXP(-Y2*Y2)
      ERFCY2= ERFC(R,EXY2)
      F2B   = YYA*(1.0-ERFCY2)+OVPI2*Y
C*
C*    INTEGRATE OVER ALL ENERGY INTERVALS ABOVE THE CURRENT ENERGY
C*    POINT.
C*
C-----NO INTERVALS ABOVE THE LAST POINT.
   60 IF(HEATME.EQ.COLD2P) GO TO 110
      SIGA  = SCOLD(HEATME)
C-----SET UP LOOP OVER INTERVALS ABOVE CURRENT ONE.
      DO 100 HEATER=HEATME,HIHEAT
      B     = XCOLD(HEATER+1)
      SIGB  = SCOLD(HEATER+1)
C-----ONLY EXTEND RANGE OF INTEGRATION UP TO ATOP UNITS ABOVE Y.
      BMY   = B-Y
      IF(BMY.LE.ATOP) GO TO 70
      XXA   = XCOLD(HEATER)**2
      XXB   = B*B
      BMY   = ATOP
      B     = Y + ATOP
      BS    = B*B
      SIGB  = TERP(BS,XXA,XXB,SIGA,SIGB)
C-----SAVE VALUES FROM LAST INTEGRAL.
   70 F2A   = F2B
C-----DEFINE CONTRIBUTION OF FIRST INTEGRAL.
      RBMY  = RATION(BMY)
      EXBMY = ZEXP(-BMY*BMY)
      ERFCBM= ERFC(RBMY,EXBMY)
C-----DEFINE CONTRIBUTION OF SECOND INTEGRAL.
      BPY   = B + Y
      IF(BPY.LE.ATOP) GO TO 80
      EXBPY = 0.0
      ERFCBP= 0.0
      GO TO 90
   80 RBPY  = RATION(BPY)
      EXBPY = ZEXP(-BPY*BPY)
      ERFCBP= ERFC(RBPY,EXBPY)
C-----ADD CONTRIBUTION FROM CURRENT INTERVAL.
   90 F2B   =YYA*(ERFCBM-ERFCBP)+OVPI*(B*(EXBMY-EXBPY)+Y*(EXBMY+EXBPY))
      DUMMY1= DUMMY1 + 0.5*(SIGA+SIGB)*(F2A-F2B)
C-----TEST FOR END OF RANGE OF INTEGRATION.
      IF(BMY.GE.ATOP) GO TO 140
  100 SIGA  = SIGB
      GO TO 130
C-----Y IS AT UPPER END OF TABULATED RANGE. DEFINE REQUIRED EXP AND ERFC
C-----TO ALLOW CROSS SECTION EXTENSION TO INFINITY.
  110 EXBMY = 1.0
      ERFCBM= 1.0
      IF(Y2.LE.ATOP) GO TO 120
      EXBPY = 0.0
      ERFCBP= 0.0
      GO TO 130
  120 RBPY  = RATION(Y2)
      EXBPY = ZEXP(-Y2*Y2)
      ERFCBP= ERFC(RBPY,EXBPY)
C-----CONTINUE CROSS SECTION AS 1/V TO XCOLD=INFINITY.
  130 DUMMY1= DUMMY1 + SCOLD(COLD2P)*XCOLD(COLD2P)*
     1 (Y*(ERFCBM+ERFCBP)+OVPI*(EXBMY-EXBPY))
C-----RE-INITIALIZE INTEGRALS TO ZERO DISTANCE VALUES
  140 F2A   = -YYA*(1.0+ERFCY2) + OVPI2*Y
C*
C*    INTEGRATE OVER ALL ENERGY INTERVALS BELOW THE CURRENT ENERGY
C*    POINT.
C*
C-----NO INTERVALS BELOW FIRST POINT.
      IF(HEATME.EQ.COLD1P) GO TO 190
      SIGB  = SCOLD(HEATME)
C-----SET UP LOOP OVER INTERVALS BELOW CURRENT POINT.
      HEATER= HEATME
      DO 180 LL=LOHEAT,HEATME
      HEATER = HEATER-1
      A      = XCOLD(HEATER)
      SIGA   = SCOLD(HEATER)
C-----ONLY EXTEND RANGE OF INTEGRATION DOWN TO ATOP UNITS BELOW Y.
      YMA    = Y-A
      IF(YMA.LE.ATOP) GO TO 150
      XXB    = XCOLD(HEATER+1)**2
      XXA    = A*A
      YMA    = ATOP
      A      = Y - ATOP
      AS     = A*A
      SIGA   = TERP(AS,XXA,XXB,SIGA,SIGB)
C-----SAVE VALUES FROM LAST INTEGRAL.
  150 F2B    = F2A
C-----DEFINE CONTRIBUTION OF FIRST INTEGRAL.
      RYMA   = RATION(YMA)
      EXYMA  = ZEXP(-YMA*YMA)
      ERFCYM = ERFC(RYMA,EXYMA)
C-----DEFINE CONTRIBUTION OF SECOND INTEGRAL.
      YPA    = Y + A
      IF(YPA.LE.ATOP) GO TO 160
      EXYPA  = 0.0
      ERFCYP = 0.0
      GO TO 170
  160 RYPA   = RATION(YPA)
      EXYPA  = ZEXP(-YPA*YPA)
      ERFCYP = ERFC(RYPA,EXYPA)
C-----ADD CONTRIBUTION FROM CURRENT INTERVAL.
  170 F2A  =-YYA*(ERFCYM+ERFCYP)+OVPI*(A*(EXYMA-EXYPA)+Y*(EXYMA+EXYPA))
      DUMMY1 = DUMMY1 + 0.5*(SIGA+SIGB)*(F2A-F2B)
C-----TEST FOR END OF RANGE OF INTEGRATION.
      IF(YMA.GE.ATOP) GO TO 220
  180 SIGB  = SIGA
      GO TO 210
C-----Y IS AT LOWER END OF TABULATED RANGE. DEFINE REQUIRED EXP AND ERFC
C-----TO ALLOW CROSS SECTION EXTENSION TO ZERO.
  190 EXYMA = 1.0
      ERFCYM= 1.0
      IF(Y2.LE.ATOP) GO TO 200
      EXYPA = 0.0
      ERFCYP= 0.0
      GO TO 210
  200 RYPA  = RATION(Y2)
      EXYPA = ZEXP(-Y2*Y2)
      ERFCYP= ERFC(RYPA,EXYPA)
C-----CONTINUE CROSS SECTION AS 1/V TO XCOLD=0.0
  210 DUMMY1= DUMMY1 + SCOLD(COLD1P)*XCOLD(COLD1P)*
     1 (Y*(ERFCYM-ERFCYP)-OVPI*(EXYMA-EXYPA))
C-----DEFINE BROADENED CROSS SECTION.
  220 SHOT(HEATME)=0.5*DUMMY1/YY
  230 CONTINUE
C
CM    HEATME=HOT2+1
      HEATME=IEND+1
C
      RETURN
      END
