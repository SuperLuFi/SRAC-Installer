C             REED                LEVEL=1        DATE=81.11.14
      SUBROUTINE REED (N,IREC,VECP,NWDS,MODE)
C
C     PERFORMS BINARY READ OPERATIONS
C
      DIMENSION IREC(1), VECP(1)
C
C     MODE IS OPERATION INDICATOR ( 1/2/3/4/5/6/7/8/9/10/11=
C     LCM TO CORE/BINARY READ/BINARY READ WITH COUNT/REWIND/
C     BINARY INTEGER READ/BINARY INTEGER READ WITH COUNT/
C     SKIP RECORDS FORWARD/SKIP WORDS WITHIN A RECORD PRIOR TO
C     INFORMATION TRANSFER/BINARY READ OF EIGHT BYTE WORDS/BINARY READ
C     OF EIGHT BYTE WORDS EXCEPT LAST WORD/BINARY READ OF FIRST EIGHT
C     BYTE WORD AND REMAINDER AS FOUR BYTE WORDS )
C
      GO TO (100,110,120,130,140,150,160,180,190,200,210), MODE
C
C     TRANSFERS INFORMATION FROM LCM TO CORE
C
C     N NOT USED
C     IREC LCM POINTER
C     VECP CORE VECTOR
C     NWDS NUMBER OF WORDS TO TRANSFER
C
  100 CONTINUE
      CALL ECRD (VECP,IREC,NWDS,IEREC)
      RETURN
C
C     READS BINARY RECORD
C
C     N IS UNIT NUMBER
C     IREC NOT USED
C     VECP IS CORE VECTOR
C     NWDS IS NUMBER OF WORDS
C
  110 CONTINUE
      READ (N)(VECP(I),I=1,NWDS)
      RETURN
C
C     READS BINARY RECORD USING FIRST WORD AS REMAINING RECORD LENGTH
C
C     N IS UNIT NUMBER
C     IREC NOT USED
C     VECP IS CORE VECTOR
C     NWDS UPON RETURN CONTAINS FIRST WORD OF RECORD
C
  120 CONTINUE
CMSASA
C     READ (N)NWDS,(VECP(I),I=1,NWDS)
C
C ... HP FORTRAN 9.16 WITH +O2 OPTIMIZATION MAY MISTAKE IN A STATEMENT
C     SUCH AS ABOVE WHEN NWDS IS A DUMMY ARGUMENT WHEN +E7 OPTION IS USED.
C     ( WORKS WELL IF, AT LEASET, NWDS IS A STATIC VARIABLE.)
C
      READ (N)IINWDS,(VECP(I),I=1,IINWDS)
      NWDS = IINWDS
C
      RETURN
C
C     REWINDS UNIT
C
C     N IS UNIT NUMBER
C     IREC NOT USED
C     VECP NOT USED
C     NWDS NOT USED
C
  130 CONTINUE
      REWIND N
      RETURN
C
C     READS BINARY INTEGER RECORD
C
C     N IS UNIT NUMBER
C     IREC IS INTEGER CORE VECTOR
C     VECP NOT USED
C     NWDS IS NUMBER OF WORDS
C
  140 CONTINUE
      READ (N)(IREC(I),I=1,NWDS)
      RETURN
C
C     READS BINARY INTEGER RECORD USING FIRST WORD AS REMAINING
C     RECORD LENGTH
C
C     N IS UNIT NUMBER
C     IREC IS INTEGER CORE VECTOR
C     VECP NOT USED
C     NWDS UPON RETURN CONTAINS THE FIRST WORD OF RECORD
C
  150 CONTINUE
CMSASA
C     READ (N)NWDS,(IREC(I),I=1,NWDS)
      READ (N)IINWDS,(IREC(I),I=1,IINWDS)
      NWDS = IINWDS
C
      RETURN
C
C     SKIPS FORWARD NWDS RECORDS WITH OPTIONAL REWIND
C
C     N IS UNIT NUMBER
C     IREC IS PRIOR REWIND INDICATOR ( 0/1=NO/YES )
C     VECP NOT USED
C     NWDS IS THE NUMBER OF RECORDS TO SKIP FORWARD
C
  160 CONTINUE
      IF (IREC(1).NE.0) REWIND N
      DO 170 IDX=1,NWDS
      READ (N)
  170 CONTINUE
      RETURN
C
C     READS RECORD SKIPPING FIRST WORDS
C
C     N IS UNIT NUMBER
C     IREC IS NUMBER OF WORDS TO SKIP
C     VECP IS CORE
C     NWDS IS NUMBER OF WORDS
C
  180 CONTINUE
      IDX=IREC(1)
      READ (N)(TEMP,I=1,IDX),(VECP(I),I=1,NWDS)
      RETURN
C
C     READS BINARY RECORD IN EIGHT BYTE WORDS
C
C     N IS UNIT NUMBER
C     IREC NOT USED
C     VECP IS CORE
C     NWDS IS NUMBER OF EIGHT BYTE WORDS
C
  190 CONTINUE
      IDX = 2 * NWDS
      READ (N)(VECP(I),I=1,IDX)
      RETURN
C
C     READS BINARY EIGHT BYTE WORDS EXCEPT FOR LAST WORD WHICH IS
C     FOUR BYTES
C
C     N IS UNIT NUMBER
C     IREC IS CORE FOR LAST WORD OF RECORD ( FOUR BYTE )
C     VECP IS CORE FOR EIGHT BYTE WORDS
C     NWDS IS NUMBER OF WORDS INCLUDING FOUR BYTE
C
  200 CONTINUE
      IDX = 2 * ( NWDS - 1 )
      IDY=1
      READ (N)(VECP(I),I=1,IDX),(IREC(I),I=1,IDY)
      RETURN
C
C     READS FIRST WORD OF RECORD AS EIGHT BYTE WORD AND REMAINDER
C     OF WORDS AS FOUR BYTE
C
C     N IS UNIT NUMBER
C     IREC IS CORE FOR EIGHT BYTE FIRST WORD
C     VECP IS CORE FOR FOUR BYTE WORDS
C     NWDS IS NUMBER OF WORDS INCLUDING EIGHT BYTE
C
  210 CONTINUE
      IDX=NWDS-1
      IDY = 2
      READ (N)(IREC(I),I=1,IDY),(VECP(I),I=1,IDX)
      RETURN
      END
