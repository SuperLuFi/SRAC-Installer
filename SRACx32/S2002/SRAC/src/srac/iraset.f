      SUBROUTINE IRASET(DNF   ,DNM   ,SIGTF  ,SIGTM  ,SLDWNF ,
     &                  SLDWNM,POT   ,AMF    ,AMM    ,SGMAF  ,
     &                  SGMAM ,DELAMF,DELAMM ,PARAF1 ,PARAM1 ,
     &                  PARAF2,PARAM2,XF     ,XM     ,YF     ,
     &                  YM    ,TRR   ,ISWF   ,ISWM   ,NISOF  ,
     &                  ENGAM ,GT    ,GN     ,GF     ,GC     ,
     &                  PEAK  ,SIGOP ,ZETINF ,BETINF ,ANS    ,ITR ,
     &                  SIGMA ,THT   ,ANSA   ,ANSP   ,PSEOUT )
C
      COMMON /IRADAT/ ISTART,NEFR,NEFB,NOMTF,NISOM,TCOR,S,NRES,
     &                NPSE,VOLF,VOLM,KNMAX,NMP,MATD(50)
      COMMON /IRAPSE/ TAKA,SQGUZ,C1B,ZETT,DELTA1
C
      DIMENSION
     & DNF(KNMAX),DNM(NISOM),SIGTF(KNMAX),SIGTM(NISOM),
     & POT(KNMAX),AMF(KNMAX),AMM(NISOM),SGMAF(KNMAX),
     & SGMAM(NISOM),DELAMF(KNMAX),DELAMM(NISOM),PARAF1(KNMAX),
     & PARAF2(KNMAX),PARAM2(NISOM),XF(KNMAX),XM(NISOM),YF(KNMAX),
     & YM(NISOM),TRR(NISOM),PARAM1(NISOM),PSEOUT(2)
      DIMENSION  ISWF(KNMAX),ISWM(NISOM),SLDWNF(KNMAX),SLDWNM(NISOM)
C
C
      ALFA(XX)=((XX-1.0)/(XX+1.0))**2
      GUZII(XX)=1.0+(XX/(1.0-XX))*ALOG(XX)
C
C
      DCTMP=DNF(NRES)
      ANS  =0.0
      IF(DCTMP.LE.0.0)  RETURN
      LENG = 6*(KNMAX+NISOM)
      CALL  CLEA(SGMAF,LENG,0.0)
      DELTA1=2.0*ENGAM/GT
      ZETINF=ZETINF*TCOR
      GA=GF+GC
      DCTMP=1.0/DCTMP
C
      DO 100 NN=1,NISOF
      SGMAF(NN)=0.0
      DELAMF(NN)=0.0
      IF(ISWF(NN).EQ.0)  GO TO 100
      SGMAF(NN)=SIGTF(NN)*DNF(NN)*DCTMP
C     IF(NN.EQ.NPSE)  SGMAF(NN)=POT(NN)
      IF(NN.EQ.NPSE)  SGMAF(NN)=PSEOUT(2)
      IF(NN.EQ.NPSE)  AMF  (NN)= 1.0
      IF(NN.EQ.NRES)  SGMAF(NN)=POT(NN)
      DELAMF(NN)=DELTA1*(1.0-ALFA(AMF(NN)))
  100 CONTINUE
C
      DCTMP=DCTMP*VOLM/VOLF
      SIGMA=0.
      DO 110 NN=1,NISOM
      SGMAM(NN)=0.0
      DELAMM(NN)=0.0
      IF(ISWM(NN).EQ.0)  GO TO 110
      SGM=DCTMP*SIGTM(NN)*DNM(NN)
      SIGMA=SIGMA+SGM
      SGMAM(NN)=SGM
      DELAMM(NN)=DELTA1*(1.0-ALFA(AMM(NN)))
  110 CONTINUE
C
      WORK=1.0
      IF(SIGMA.NE.0.0)  WORK=1.0/SIGMA
      DO 120 NN=1,NISOM
      TRR(NN)=SGMAM(NN)*WORK
  120 CONTINUE
C
C     IR-PARAMETER INITIAL SET
C
      ISW1=0
      ITR =0
      ETA =0.0001
  200 CONTINUE
C
      DO 210 NN=1,NISOF
      PARAF1(NN)=1.0
      IF(NN.EQ.NPSE)  PARAF1(NN)  = PSEOUT(1)
  210 CONTINUE
      IF(ISW1.EQ.0)   PARAF1(NRES)= 0.0
C
      DO 220 NN=1,NISOM
      PARAM1(NN)=1.0
  220 CONTINUE
C
C     ITERATION LOOP
C
  230 CONTINUE
      DO 240 NN=1,NISOF
      PARAF2(NN)=PARAF1(NN)
  240 CONTINUE
      DO 250 NN=1,NISOM
      PARAM2(NN)=PARAM1(NN)
  250 CONTINUE
C
      THT=0.0
      DO 260 NN=1,NISOM
      IF(ISWM(NN).EQ.0)  GO TO 260
      THT=THT+TRR(NN)*PARAM1(NN)
  260 CONTINUE
      TAKA=THT*SIGMA/(THT*SIGMA+(1.0-THT)*S)
C
      WORK=TAKA*S
      DO 270 NN=1,NISOF
      IF(ISWF(NN).EQ.0)  GO TO 270
      WORK=WORK+PARAF1(NN)*SGMAF(NN)
  270 CONTINUE
C
      ZETT =PARAF1(NRES)*SIGOP*TCOR/WORK
      BETT =1.0+(PEAK*(GA+PARAF1(NRES)*GN))/GT/WORK
      GUZAI=ABS(BETT-ZETT*ZETT)
      SQGUZ=SQRT(GUZAI)
      COB  =BETINF-BETT+2.0*ZETT*(ZETT-ZETINF)
      C1B  =1.0-BETT+2.0*ZETT*ZETT
C
C     IR-PARAMETER SEARCH
C
      DO 280 NN=1,NISOF
      Z =0.5*DELAMF(NN)/SQGUZ
      ZZ=Z*Z
      XF(NN)=1.0
      YF(NN)=1.0
      IF(Z.LE.0.001)  GO TO 280
      XF(NN)=ATAN(Z)/Z
      YF(NN)=ALOG(1.0+ZZ)/ZZ
  280 CONTINUE
C
      DO 290 NN=1,NISOM
      Z=0.5*DELAMM(NN)/SQGUZ
      ZZ=Z*Z
      XM(NN)=1.0
      YM(NN)=1.0
      IF(Z.LE.0.001)  GO TO 290
      XM(NN)=ATAN(Z)/Z
      YM(NN)=ALOG(1.0+ZZ)/ZZ
  290 CONTINUE
C
      DO 320 NN=1,NISOF
      IF(ISWF(NN).EQ.0)          GO TO 320
      IF(NN.EQ.NRES)  GO TO 300
      IF(NN.EQ.NPSE)             GO TO 320
C     IF(NN.NE.NPSE.AND.SLDWNF(NN).LT.0.0001)   GO TO 320
CM    IF(SLDWNF(NN).LT.0.0001)   GO TO 320
      IF(SLDWNF(NN).LT.0.00001)  GO TO 320
C
      WORK=1.0-XF(NN)+DELAMF(NN)*ZETT*YF(NN)*0.5/C1B
      GO TO 310
C
  300 CONTINUE
      WORK=1.0-XF(NN)+DELAMF(NN)*YF(NN)*(ZETT-ZETINF)*0.5/COB
C
  310 CONTINUE
       PARAF1(NN)=0.5*(PARAF2(NN)+WORK)
  320 CONTINUE
C
      DO 330 NN=1,NISOM
      IF(ISWM(NN).EQ.0)  GO TO 330
CM    IF(SLDWNM(NN).LE.0.0001)   GO TO 330
      IF(SLDWNM(NN).LE.0.00001)  GO TO 330
      WORK=1.0-XM(NN)+0.5*DELAMM(NN)*YM(NN)*ZETT/C1B
      PARAM1(NN)=0.5*(PARAM2(NN)+WORK)
  330 CONTINUE
C
C     CONVERGENCE CHECK
C
      DO 340 NN=1,NISOF
      IF(ISWF(NN).EQ.0)  GO TO 340
      PRTMP=PARAF1(NN)
      RRR  =ABS((PRTMP-PARAF2(NN))/PRTMP)
      IF(RRR.GT.ETA)  GO TO 360
  340 CONTINUE
      DO 350 NN=1,NISOM
      IF(ISWM(NN).EQ.0)  GO TO 350
      PRTMP=PARAM1(NN)
      RRR  =ABS((PRTMP-PARAM2(NN))/PRTMP)
      IF(RRR.GT.ETA)  GO TO 360
  350 CONTINUE
      GO TO 370
C
  360 CONTINUE
      ITR=ITR+1
CM    IF(ITR.GT.50)  GO TO 370
CM    IF(ITR.NE.25)  GO TO 230
CMOD  11/5/1985
      IF(ITR.GT.100)  GO TO 370
      IF(ITR.NE.50)  GO TO 230
CEND
      ISW1=1
      GO TO 200
C
  370 CONTINUE
      ANS =TAKA*S
      ANSP=TAKA*S
      ANSA=0.0
      DO 380 NN=1,NISOF
      IF(ISWF(NN).EQ.0)  GO TO 380
      ANSP=ANSP+PARAF1(NN)*SGMAF(NN)
      IF(NN.EQ.NRES)  GO TO 380
      ANS =ANS +PARAF1(NN)*SGMAF(NN)
      ANSA=ANSA+PARAF1(NN)*SGMAF(NN)
  380 CONTINUE
C
C
C     IR-PARAMETER SEARCH END
C
      RETURN
      END
