C             INITAL              LEVEL=4        DATE=84.04.10
      SUBROUTINE INITAL ( C,WGT,COSMU,COSETA,WMU,WETA,P1,P2,P3,P4,CHI,
     &CHIA,VEL,MIXNUM,MIXCOM,MIXDEN,XRAD,YRAD,IDX,IDY,IDYA,XH,YH,XRADA,
     &YRADA,A1,A2,A3,A4,A5,FISSA,XM,YM,IHX,IHY,QG,FLUX,IDCS,FG,AAJ,AL1,
     &AL2,B1,CTOT,XDF,YDF,IHM,MT,NM,MM,ITJT,IT,JT )
C
      COMMON /TW1C/ DD(1),LIM1,IA(210)
      COMMON /WORK/AAA(1),LIM2,AA(130)
      DIMENSION D(212),A(132)
      EQUIVALENCE (D(1),DD(1)),(AAA(1),A(1))
      EQUIVALENCE (D(112),IPFX),(D(115),IPXS),(D(116),IPXSCT),
     &(D(125),I3),(D(119),LTAXS),(D(113),LTFX),(D(118),LTOXS),
     &(D(117),LTXS),(D(114),LXFX),(D(151),NEXTER),(D(157),NOUT)
C
      DIMENSION C(IHM,MT),WGT(1),COSMU(1),COSETA(1),WMU(1),WETA(1),
     &P1(NM,MM),P2(NM,MM),P3(NM,MM),P4(NM,MM),CHI(1),CHIA(1),VEL(1),
     &MIXNUM(1),MIXCOM(1),MIXDEN(1),XRAD(1),YRAD(1),IDX(1),IDY(1),
     &IDYA(1),XH(1),YH(1),XRADA(1),YRADA(1),A1(1),A2(1),A3(1),A4(1),
     &A5(1),FISSA(ITJT),XM(1),YM(1),IHX(1),IHY(1),QG(1),FLUX(NM,ITJT),
     &IDCS(1),FG(1),AAJ(1),AL1(1),AL2(1),B1(1),CTOT(IT,JT),XDF(1),YDF(1)
      DIMENSION NA(1)
      DIMENSION ER(6), ER1(4), ER2(4), ER3(6), ER4(8)
C
C     MATERIAL MESH INDEXING
C
CKSK  EQUIVALENCE (D(1),NA(1))
      EQUIVALENCE (DD(1),NA(1))
      EQUIVALENCE (IA(25),IMC),(IA(27),JMC),(IA(173),LIHXC),
     &(IA(174),LIHYC),(IA(175),LDXC),(IA(176),LDYC),(IA(177),LDYAC),
     &(IA(80),MESH)
C
      REAL MIXDEN
      INTEGER G,H,OITNO
C
      EQUIVALENCE (IA(1),ITH),(IA(4),IGM),(IA(10),IBT),(IA(11),IEVT),
     &(IA(12),ISTART),(IA(14),MIN),(IA(15),MS),(IA(16),IHT),
     &(IA(23),IPVT),(IA(29),IXM),(IA(30),IYM),(IA(32),IGEOM),
     &(IA(37),EV),(IA(39),PV),(IA(50),BHGT),(IA(60),IP),(IA(61),JP),
     &(IA(62),IGP),(IA(71),ISPANC),(IA(73),ISPANF),(IA(77),ITP),
     &(A(61),LC),(A(76),LFL),(A(109),LVEL),(A(30),FTP),(A(31),IFN),
     &(A(32),OITNO),(A(40),SUMMU),(A(41),SUMETA),(D( 83),NN)
      EQUIVALENCE (IA(5),IM),(IA(6),JM),(A(67),LQ),(A(68),LQR1),
     &(A(69),LQR2),(A(70),LQT1),(A(71),LQT2),(A(87),LQQG),(A(117),LQB1),
     &(A(118),LQB2)
C
CSASA REAL*8 ER,ER1,ER2,ER3,ER4,XTEMP
      REAL*8                    XTEMP
      CHARACTER*8 ER,ER1,ER2,ER3,ER4
C
      DATA ER/'MIX NO','. GREA','TER TH','AN TAB','LE LEN','GTH   '/
      DATA ER1/'RADII ','DO NOT',' INCRE','ASE   '/
      DATA ER2/'AXII D','O NOT ','INCREA','SE    '/
      DATA ER3/'THETA ','GREATE','R THAN',' ONE R','EVOLUT','ION   '/
      DATA ER4/ 'THETA ','EQUAL ','ONE RE','VOLUTI','ON FOR',' IBT N',
     &'OT THR','EE    '/
C
C     SET TRIGGER FOR FISSION CALCULATION
C
      IFN=1
      IF (OITNO.GT.0) GO TO 140
C
C     GENERATE TWO SPECIAL ARRAYS FOR CALCULATING CURRENTS
C
      SUMMU=0.0
      SUMETA=0.0
      DO 100 M=1,MM
      WMU(M)=COSMU(M)*WGT(M)
      WETA(M)=COSETA(M)*WGT(M)
      SUMMU=SUMMU+WMU(M)
  100 SUMETA=SUMETA+WETA(M)
C
C     GENERATE ALPHA COEFFICIENTS
C
      M=1
      DO 120 N=1,NN
      TP=0.0
      DO 110 J=1,N
      T=TP+WGT(M)*COSMU(M)
      AL1(M)=T/WGT(M)
      AL2(M)=TP/WGT(M)
      TP=T
  110 M=M+1
  120 CONTINUE
C
C     INVERT GROUP ORDER OF FISSION SPECTRUM AND VELOCITIES
C     IF ADJOINT PROBLEM
C
      IF (ITH.EQ.0) GO TO 140
      G=1
      H=IGM
  130 T=CHI(G)
      CHI(G)=CHI(H)
      CHI(H)=T
      T=VEL(G)
      VEL(G)=VEL(H)
      VEL(H)=T
      G=G+1
      H=H-1
      IF (G.LT.H) GO TO 130
C
C     MIX CROSS SECTIONS IF 1) THERE ARE ANY MIXTURES AND
C     2) EITHER THIS IS THE INITIAL ENTRY OR IEVT=3
C
  140 IF (MS.EQ.0) GO TO 220
      IF ((OITNO.GT.0).AND.(IEVT.NE.3)) GO TO 220
      IF (OITNO.GT.0) GO TO 150
      WRITE (NOUT,560)(MIXNUM(I),MIXCOM(I),MIXDEN(I),I,I=1,MS)
  150 CONTINUE
      DO 210 G=1,IGM
      CALL REED (0,IPXS+(G-1)*LTXS,C,LTAXS,1)
      DO 185 M=1,MS
      N=MIXNUM(M)
      L=MIXCOM(M)
      AD=MIXDEN(M)
      IF ((N.GT.MT).OR.(L.GT.MT)) CALL ERROR (1,ER,6)
      DO 180 I=1,IHM
      IF (L.EQ.0) GO TO 170
      IF ((AD.EQ.0.0).AND.(IEVT.EQ.3)) GO TO 160
      C(I,N)=C(I,N)+AD*C(I,L)
      IF (I.EQ.1) AAJ(N)=AAJ(N)+AD*AAJ(L)
      GO TO 180
  160 C(I,N)=EV*C(I,N)
      IF (I.EQ.1) AAJ(N)=EV*AAJ(N)
      GO TO 180
  170 C(I,N)=AD*C(I,N)
      IF (I.EQ.1) AAJ(N)=AD*AAJ(N)
  180 CONTINUE
  185 CONTINUE
      IF (OITNO.NE.0) GO TO 200
      IF (I3.EQ.2) GO TO 200
      IF (MT.GT.MIN) GO TO 190
      WRITE (NOUT,570)G
      CALL WWRITE (2,2,C,IHM,MIN,1,'      ','      ','      ','MATERL')
      GO TO 200
  190 WRITE (NOUT,580)G
      MINP=MIN+1
       XTEMP = DFLOAT(MINP)
      CALL WWRITE (5,2,C,IHM,MT,1,XTEMP,'MIXED ','X-SECT','MATERL')
  200 CONTINUE
      CALL RITE (0,IPXS+(G-1)*LTXS,C,LTAXS,1)
  210 CONTINUE
C
C     MODIFY COARSE MESH BOUNDARIES IF IEVT.EQ.4
C
  220 IF (OITNO.NE.0) GO TO 250
      IF (MESH.NE.0) GO TO 250
      DO 230 I=1,IP
  230 XRADA(I)=XRAD(I)
      DO 240 J=1,JP
  240 YRADA(J)=YRAD(J)
  250 IF (IEVT.NE.4) GO TO 290
      IF (IXM.EQ.0) GO TO 270
      DO 260 I=1,IMC
  260 XRADA(I+1)=XRADA(I)+(XRAD(I+1)-XRAD(I))*(1.+EV*XM(I))
  270 IF (IYM.EQ.0) GO TO 290
      DO 280 J=1,JMC
  280 YRADA(J+1)=YRADA(J)+(YRAD(J+1)-YRAD(J))*(1.+EV*YM(J))
C
C     CHECK RADIAL AND AXIAL VALUES
C
  290 DO 300 I=1,IMC
      IF (XRADA(I+1).GT.XRADA(I)) GO TO 300
      CALL ERROR (4,ER1,4)
      GO TO 550
  300 CONTINUE
      DO 310 J=1,JMC
      IF (YRADA(J+1).GT.YRADA(J)) GO TO 310
      CALL ERROR (4,ER2,4)
      GO TO 550
  310 CONTINUE
      IF (IGEOM.NE.3) GO TO 350
      IF (YRADA(JMC+1)-1.0) 340,320,330
  320 IF (IBT.EQ.3) GO TO 340
      CALL ERROR (4,ER4,8)
      GO TO 550
  330 CALL ERROR (4,ER3,6)
      GO TO 550
  340 CONTINUE
  350 IF (OITNO.EQ.0) GO TO 360
      IF (IEVT.NE.4) GO TO 450
C
C     GENERATE REBALANCE MESH INDICES
C
  360 K=1
      L=IHX(1)
      DO 370 I=1,IT
      IF (I.LE.L) GO TO 370
      K=K+1
      L=L+IHX(K)
  370 IDX(I)=K
      K=1
      L=IHY(1)
      DO 390 I=1,JT
      IF (I.LE.L) GO TO 380
      K=K+1
      L=L+IHY(K)
  380 IDY(I)=(K-1)*IM
  390 IDYA(I)=K
C
C     GENERATE INTERVAL SPACING
C
      DO 400 I=1,IMC
  400 XH(I)=(XRADA(I+1)-XRADA(I))/NA(LIHXC+I-1)
      DO 410 J=1,JT
      JA=NA(LDYAC+J-1)
  410 YH(J)=(YRADA(JA+1)-YRADA(JA))/NA(LIHYC+JA-1)
      RI=XRADA(1)
      DO 420 I=1,IT
      IB=NA(LDXC+I-1)
      RIP=XH(IB)+RI
      T=(RIP**2-RI**2)*3.1415926536
      A3(I)=XH(IB)
      IF (IGEOM.EQ.2) A3(I)=T
      A4(I)=6.2831853*RI
      IF (IGEOM.EQ.1) A4(I)=1.0
      A5(I)=T
      IF (IGEOM.EQ.1) A5(I)=XH(IB)
  420 RI=RIP
      A4(ITP)=6.2831853*RI
      IF (IGEOM.EQ.1) A4(ITP)=1.0
      DO 430 I=1,IT
      A1(I)=A4(I)+A4(I+1)
  430 A2(I)=A4(I+1)-A4(I)
      DO 440 J=1,JT
  440 B1(J)=1.0/YH(J)
C
C     PREPARE FISSION SPECTRUM
C
  450 IF (OITNO.GT.0) GO TO 470
      CHIA(IGP)=0.
      IF(IPVT.EQ.1) PVINV=1./PV
      DO 460 G=1,IGM
      T=CHI(G)
      IF (IPVT.EQ.1) T=T*PVINV
      CHIA(G)=T
      CHI(G)=T
  460 CHIA(IGP)=CHIA(IGP)+CHIA(G)
      CHI(IGP)=CHIA(IGP)
      WRITE (NOUT,590)(I,CHIA(I),VEL(I),I=1,IGP)
C
C     IF THERE ARE DISTRIBUTED OR BOUNDARY SOURCES,
C     COMPUTE THEIR VOLUME (SURFACE) INTEGRAL AND
C     ENERGY INTEGRAL   NORMALIZE AS REQUIRED
C
  470 CALL CLEARW(0.0,QG,IGP)
      IF (IEVT.GT.0) GO TO 480
CKSK  CALL INITQ ( IDX,IDYA,A(LQ),QG,A(LQQG),YH,A4,A5,WMU,WETA,A(LQR1),
CKSK &A(LQR2),A(LQT1),A(LQT2),A(LQB1),A(LQB2),A3,NM,ITJT,IM,JM,IT,JT,
CKSK &MM )
      CALL INITQ ( IDX,IDYA,AAA(LQ),QG,AAA(LQQG),YH,A4,A5,WMU,WETA,
     &AAA(LQR1),AAA(LQR2),AAA(LQT1),AAA(LQT2),AAA(LQB1),AAA(LQB2),
     &A3,NM,ITJT,IM,JM,IT,JT,MM )
      IF (NEXTER.NE.0) GO TO 550
C
C     CALCULATE FISSION SOURCE (NO NORMALIZATIONS PERFORMED
C     HERE - NEXT CALL IN MAIN IS FISCAL) IF FIRST OUTER
C     AND ISTART=0, USE FLAT FISSION GUESS
C
  480 IFLAG=1
      IF ((ISTART+OITNO).EQ.0) IFLAG=0
      CALL CLEARW(0.0,FISSA,ITJT)
      FTP=FG(IGP)
CKH
CKH   WRITE(6,600)
CK600 FORMAT(1H0,' CHECKING OUTPUT FROM INITAL')
      DO 540 G=1,IGM
      IF (IFLAG.EQ.0) GO TO 490
      CALL REED (0,IPFX+(G-1)*LTFX,FLUX,LXFX,1)
CKH
CKH   WRITE(6,610) G,(FLUX(1,IIJJ),IIJJ=1,LXFX)
CK610 FORMAT(' G=',I2,2X,'FLUX(I)'/(10E13.5))
  490 CONTINUE
      CALL REED (0,IPXS+(G-1)*LTXS,C,LTOXS,1)
CKH
CKH   WRITE(6,620) G,((C(III,JJJ),III=1,IHM),JJJ=1,MT)
CK620 FORMAT(' G=',I2,2X,'C(I)'/(11E11.3))
      DO 510 J=1,JT
      DO 500 I=1,IT
      IZ=NA(LDXC+I-1)+NA(LDYC+J-1)
      IC=IABS(IDCS(IZ))
      T=C(IHT,IC)
      IF (BHGT.GT.0.0) T=T+T*(1.8138/(BHGT*T+1.4209))**2
C
C     EXTRAPOLATION DISTANCE NOT INCLUDE
C
      IF (BHGT.LT.0.0) T=T+T*(1.8138/(BHGT*T))**2
CKSK  IF (IEVT.EQ.2) T=T+EV/A(LVEL+G-1)
      IF (IEVT.EQ.2) T=T+EV/AAA(LVEL+G-1)
CKSK  IF (IPVT.EQ.2) T=T+PV/A(LVEL+G-1)
      IF (IPVT.EQ.2) T=T+PV/AAA(LVEL+G-1)
      CTOT(I,J)=T*XDF(I)*YDF(J)*A5(I)
  500 CONTINUE
  510 CONTINUE
      CALL RITE (0,IPXSCT+(G-1)*LTXS,CTOT,ITJT,1)
      IF (IFLAG.EQ.0) GO TO 540
      DO 535 J=1,JT
      JA=(J-1)*IT
      DO 530 I=1,IT
      IF (ITH.EQ.0) GO TO 520
      FISSA(I+JA)=FISSA(I+JA)+CHIA(G)*FLUX(1,I+JA)
CKH
CKH   WRITE(6,611) FISSA(I+JA),CHIA(G),FLUX(1,I+JA)
CK611 FORMAT(1X,'FISSA=',E12.5,2X,'CHIA=',E12.5,2X,'FLUX=',E12.5)
      GO TO 530
  520 IZ=NA(LDXC+I-1)+NA(LDYC+J-1)
      K=IABS(IDCS(IZ))
CKH
CKH   WRITE(6,630) I,J,IZ,K,IHT,JA,C(IHT-1,K),XDF(I),YDF(J),FLUX(1,I+JA)
CK630 FORMAT(1X,'I=',I2,2X,'J=',I2,2X,'IZ=',I2,2X,'K=',I2,2X,'IHT=',I2,
CKH  1       2X,'JA=',I4,2X,4E13.5)
      FISSA(I+JA)=FISSA(I+JA)+C(IHT-1,K)*XDF(I)*YDF(J)*FLUX(1,I+JA)
  530 CONTINUE
  535 CONTINUE
  540 CONTINUE
      IF (IFLAG.EQ.0) CALL CLEARW(1.0,FISSA,ITJT)
  550 RETURN
C
  560 FORMAT (1H0/' MIXTURE NUMBER ','MIXTURE COMMAND ',' MATERIAL A',
     &'TOMIC DENSITY'/(4X,I8,8X,I8,8X,1P,E16.7,8X,I8))
  570 FORMAT ('0MIXED X-SECTIONS FOR GROUP ',I3)
  580 FORMAT ('0GROUP NUMBER ',I3)
  590 FORMAT (1H0/' GROUP ','FISSION SPECTRUM','    VELOCITIES'//(I4,3X,
     &1P2E16.7))
      END
