CKDUE --119 ***CITATION*** RESIDUE CALC. FOR 3-D/ CF-KLUX
C
      SUBROUTINE KDUE(SCAC, RESLM,RESSA, P2E,NRGNE, SOURE,DCONRE,DCONBE,
     &  DCONBK,XI, IVX,JVX,KBVX,KVX,LVX,IVXP1,JVXP1, KBVXP1,IVZ,KVZ,
     & JIVX,JIP1VX,JP1IXZ,IOVX,IOVZ,A,MEMORY,AIO,IX3738, XLAMDA,SIG,
     & PVOL,NCOMP,MVX)
C
CDEL  INTEGER RGX , MSX , ZNEX , ZDX , WZX
CDEL  PARAMETER ( RGX=100, MSX=211, ZDX=200, ZNEX=1000, WZX=100 )
      INCLUDE  'CITPMINC'
C
      REAL*8 XLAMDA,XLD
      REAL *8 TTT1, TTT2, TTT3, TTT4, TT1, TT2, TT3, TT4, TT5, TT6
C
      COMMON/ALSUB/BLSUB(30),TITL1(18),TITL2(18),IMAX,JMAX,KBMAX,KMAX,
     & LMAX,MMAX, NMAX,IMXP1,JMXP1,KBMXP1,NSETMX,NTO,MM1VX,KM1VX,IOIN,
     & IOUT,IOSIG,IOFLX,IO1,IO2,IO3,IO4,IO7,NER(100), IX(200),INNO(100),
     &  NGC(24),IEDG(24),ITMX(24),TIMX(6), GLIM(6),NDPL(24),IEDP1(24),
     & IEDP2(24),IEDP3(24), DPLH(6),NUAC(24),EPI(6),XMIS(6),NSRH(24),
     & XSRH1(6), XTR1(WZX),XTR2(WZX),NXTR1(WZX),NXTR2(WZX),SPARE(200),
     & IXPUT(200),XPUT(200)
      COMMON/AFLUX/BFLUX(30),KXMN8,NIT,NIIT,NIIIT,JXP1,KSCT1,KSCT2,
     & ISTART,IEP, VRGP1,VRGP2,VRGP3,VRG1,VRG2,VRGK1,VRGK2,XABS,PROD,
     & XLEK,RMX,RMN,XKEF1,XKEF2,XKEF3,EXFC1,EXFC2,EXFC3, NI3,IEXTR,
     & IRECV,VRGABS,LO3,LO4,XLAMDB,EPI1,EPI2, BETTA,SUMXI,IX25,IX28,I,J,
     &  KB,K,ITMAX,ITIME, BET(MSX),DEL(MSX)
C
      DIMENSION SCAC(KVX,MVX,KVX), P2E(JIVX ,KBVX,KVX), NRGNE(JVX,IVX,
     & KBVX),SOURE(JVX,IVX,KBVX), DCONRE(JP1IXZ ,KBVX, IOVZ),
     & DCONBE(JIP1VX ,KBVX,IOVX),DCONBK(JIVX ,KBVXP1,IOVX), XI(KVX,MVX)
      DIMENSION A(MEMORY)
      DIMENSION AIO(IX3738)
      DIMENSION SIG(KVX,MVX,10),PVOL(LVX),NCOMP(LVX)
C
CCCCC ********* SUBSCRIPT DEFINITIONS (KDUE E-200) ********* CCCCC
C    NEW         OLD            NEW         OLD
C     N1         J,I             N6       J-1,I+1
C     N2         J,I-1           N7       J+1,I-1
C     N3         J,I+1           N8 *       J,I
C     N4       J-1,I             N9 *       J,I+1
C     N5       J+1,I             N10 *    J+1,I
C     N11        1,I             N12      JVX,I
C     N13      JVX,J             N14        I,IVX
C     N15      JVX,IVXP1-I
C
C     INRB = 1  ORDINARY
C     INRB = 2  PERIODIC(REPEATING)
C     INRB = 3  90 DEGREE ROTATIONAL
C     INRB = 4  180 DEGREE ROTATIONAL
C
      INRB = IX(72) + 1
      KMAXP1 = KMAX + 1
      IX37 = IX(37)
      IX38 = IX(38)
      IO15 = IX(82)
      IOADJ = IO15
      IF (IX(71).GT.0) IOADJ = IO2
      IF (IX37.GT.0) REWIND IOADJ
      TTT1 = 0.0
      TTT2 = 0.0
      TTT3 = 0.0
      TTT4 = 0.0
      IF ((IX(24).EQ.0).OR.(IX(17).EQ.0)) GO TO 100
      XLA = 1.0/SPARE(50)
      XLD = XLAMDA
      IF (IX(17).GE.1) XLD = 0.0
      GO TO 103
  100 CONTINUE
C******************************SEARCH OPTIONS***************************
      IF ((IX(5).EQ.0).OR.(IX(5).GE.2)) GO TO 102
  101 XLD = XLAMDA
      IF (IX(5).EQ.1) XLD = 0.0
      XLA = 1.0/SPARE(50)
      GO TO 103
  102 XLD = 0.0
      XLA = XLAMDA
C     CONTINUE
  103 CONTINUE
      DO 129 KT1=1,KMAX
      IF (IX37.EQ.0) GO TO 106
      READ(IOADJ) AIO
      IF (IX(71).GT.0) GO TO 104
      K = KT1
      GO TO 105
  104 K = KMAXP1 - KT1
  105 N = 1
      GO TO 109
  106 CONTINUE
      IF (IX(24).GT.0) GO TO 107
      K = KT1
      GO TO 108
  107 K = KMAXP1 - KT1
  108 N = K
  109 CONTINUE
      DO 128 KB = 1,KBMAX
      DO 127 I = 1,IMAX
      NN1= (I-1)*JVX
      NN2= (I-1)*JVXP1
      DO 126 J = 1,JMAX
      NOE = J-(J/2)*2
      N1= NN1 + J
      N2= N1 - JVX
      N3= N1 + JVX
      N4= N1 - 1
      N5= N1 + 1
      N6= N3 - 1
      N7= N2 + 1
      N8= NN2 + J
      N10= N8 + 1
      T1 = P2E(N1 ,KB,K)
      IF (T1.EQ.0.0) GO TO 126
      L = NRGNE(J,I,KB)
      M = NCOMP(L)
      TT5 = 0.0
      IF (IX(24).EQ.0) GO TO 111
      DO 110 KK = 1,KMAX
      TT5 = TT5 + SCAC(K,M,KK)*P2E(N1,KB,KK)
  110 CONTINUE
      GO TO 113
  111 CONTINUE
      DO 112 KK = 1,KMAX
      TT5 = TT5 + SCAC(KK,M,K)*P2E(N1,KB,KK)
  112 CONTINUE
  113 CONTINUE
      TT1 = (SIG(K,M,3) + SIG(K,M,9) + XLD*SIG(K,M,5))*T1*PVOL(L)
      TT3 = SIG(K,M,2)*T1
      TDR = DCONRE(N10 ,KB,N)
      IF (TDR.EQ.4096.0E-13) TDR = 0.0
      T2 = DCONBE(N1,KB,N)
      T3 = DCONBE(N3,KB,N)
      IF (NUAC(5).NE.14) GO TO 115
      T2 = 0.0
      T3 = 0.0
      IF (NOE.EQ.0) GO TO 114
      N2 = N2+1
      T2 = DCONBE(N1,KB,N)
      GO TO 115
  114 CONTINUE
      N3 = N3-1
      T3 = DCONBE(N3,KB,N)
  115 CONTINUE
      P2EUT = P2E(N2 ,KB,K)
      P2EUB = P2E(N3 ,KB,K)
      P2EUL = P2E(N4 ,KB,K)
      P2EUR = P2E(N5 ,KB,K)
      P2EUF = P2E(N1 ,KB-1,K)
      P2EUK = P2E(N1 ,KB+1,K)
      IF (I.EQ.1) P2EUT = 0.0
      IF (I.NE.IMAX) GO TO 116
      P2EUB = 0.0
      IF (INRB.NE.3) GO TO 116
      N13 = J*JVX
      IF (NUAC(5).EQ.14) N13 = N13/2
      P2EUB = P2E(N13,KB,K)
  116 CONTINUE
      IF (J.NE.1) GO TO 117
      P2EUL = 0.0
      IF (INRB.NE.2) GO TO 117
      N12 = NN1 + JMAX
      P2EUL = P2E(N12 ,KB,K)
  117 IF (J.NE.JMAX) GO TO 121
      P2EUR = 0.0
      GO TO (121,118,119,120),INRB
  118 CONTINUE
      N11 = NN1 + 1
      P2EUR = P2E(N11,KB,K)
      GO TO 121
  119 CONTINUE
      N14 = (IVX-1)*JVX + I
      IF (NUAC(5).EQ.14) N14 = N14+I
      P2EUR = P2E(N14,KB,K)
      GO TO 121
  120 CONTINUE
      N15 = (IVXP1-I)*JVX
      P2EUR = P2E(N15,KB,K)
  121 CONTINUE
      IF (KB.EQ.1) P2EUF = 0.0
      IF (KB.EQ.KBMAX) P2EUK = 0.0
      IF (NUAC(5).EQ.13) GO TO 122
      TT4 = (T1-P2EUT )*T2 + (T1-P2EUB )*T3 + (T1-P2EUL )*DCONRE(N8 ,KB,
     &  N)+ (T1-P2EUR)*TDR+ (T1-P2EUF )* DCONBK(N1 ,KB,N)+ (T1-P2EUK )*
     & DCONBK(N1 ,KB+1,N)
      GO TO 123
C     3-D HEXAGONAL RESIDUES
  122 KKK = N + IOVX
      N9= N8 + JVXP1
      P2EUH=P2E(N6 ,KB,K)
      P2EUI=P2E(N7 ,KB,K)
      IF (I.EQ.1.OR.J.EQ.JMAX) P2EUI=0.0
      IF (J.EQ.1.OR.I.EQ.IMAX) P2EUH=0.0
      TT4 = (T1-P2EUT )*DCONBE(N1 ,KB,N)+ (T1-P2EUB )*DCONBE(N3 ,KB,N) +
     &  (T1-P2EUL )*DCONRE(N8 ,KB,N)+ (T1-P2EUR)*TDR+ (T1-P2EUF )*
     & DCONBK(N1 ,KB,N)+ (T1-P2EUK )*DCONBK(N1 ,KB+1,N) + (T1-P2EUH)*
     & DCONRE(N9 ,KB,KKK)+ (T1-P2EUI)*DCONRE(N10 ,KB,KKK)
  123 CONTINUE
      IF (IX(24).EQ.0) GO TO 124
      TT6 = SIG(K,M,4)*PVOL(L)*SOURE(J,I,KB)*XKEF1
      GO TO 125
C 124 TT6 = XI(K)*SOURE(J,I,KB) + XLD*SIG(K,M,8)*PVOL(L)
  124 TT6 = XI(K,M)*SOURE(J,I,KB) + XLD*SIG(K,M,8)*PVOL(L)
  125 CONTINUE
      TT3=PVOL(L)*(TT3-TT5)
      TTT1 = TTT1 + TT6*(TT4+TT1+TT3)
      TTT2 = TTT2 +TT6**2
      TTT3 = TTT3 + TT1*(XLA*TT6-TT4-TT3)
      TTT4 = TTT4 + TT1**2
  126 CONTINUE
  127 CONTINUE
  128 CONTINUE
  129 CONTINUE
      IF (IX37.GT.0) REWIND IOADJ
      RESLM = TTT2/TTT1
      RESSA = TTT3/TTT4
      IF (NUAC(3).LE.0) GO TO 130
      XKEF2 = XKEF1
      XKEF1 = RESLM
      XLAMDA = 1.0/XKEF1
      VRGK1 = ABS(XKEF2/XKEF1-1.0)
  130 CONTINUE
      RETURN
      END
