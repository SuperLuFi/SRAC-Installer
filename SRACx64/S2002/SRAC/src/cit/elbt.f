CELBT --- PROMPT NEUTRON LIFE TIME AND EFFECTIVE BETA'S / CF-OUTC
C
      SUBROUTINE ELBT(P2E,P1E,NRGNE,NCOMP,SIG,P2,XII,SOURE,UTIL,UTILE,
     1                PVOL,BVSIGF,DXII,XI,IVX,JVX,KBVX,KVX,LVX,MVX,
     2                JIVX,IDELAY,BLSIGF                               )
C
CDEL  INTEGER RGX , MSX , ZNEX , ZDX , WZX
CDEL  PARAMETER ( RGX=100, MSX=211, ZDX=200, ZNEX=1000, WZX=100 )
      INCLUDE  'CITPMINC'
C
      REAL*8 XII,P2,BIGD,PA,PF,T1,XLIFE,SUM,SUML,BIEFF,BLEFF
C
COMM  XII  FISSION SPECTRUM DIVIDED BY KEFF  IN ESET  1989/6/20
C
      COMMON/ALSUB/BLSUB(30),TITL1(18),TITL2(18),IMAX,JMAX,KBMAX,KMAX,
     & LMAX,MMAX, NMAX,IMXP1,JMXP1,KBMXP1,NSETMX,NTO,MM1VX,KM1VX,IOIN,
     & IOUT,IOSIG,IOFLX,IO1,IO2,IO3,IO4,IO7,NER(100), IX(200),INNO(100),
     &  NGC(24),IEDG(24),ITMX(24),TIMX(6), GLIM(6),NDPL(24),IEDP1(24),
     & IEDP2(24),IEDP3(24), DPLH(6),NUAC(24),EPI(6),XMIS(6),NSRH(24),
     & XSRH1(6), XTR1(WZX),XTR2(WZX),NXTR1(WZX),NXTR2(WZX),SPARE(200),
     & IXPUT(200),XPUT(200)
C
      DIMENSION P2E(JIVX,KBVX,KVX),P1E(JIVX,KBVX),NRGNE(JVX,IVX,KBVX),
     &          NCOMP(LVX),SIG(KVX,MVX,12),P2(JVX,IVX,KVX),XII(KVX,MVX),
     &          SOURE(JVX,IVX,KBVX),UTIL(JVX,IVX),UTILE(JIVX,KBVX),
     &          PVOL(LVX),BVSIGF(KVX,IDELAY,MVX),DXII(KVX,IDELAY,MVX),
     &          XI(KVX,MVX)
      DIMENSION BLSIGF(KVX,IDELAY,MVX)
      DIMENSION BIEFF(15),BLEFF(15)
C
      IO26 = IX(93)
      IO18 = IX(85)
C
      CALL ESET(P1E,P2E,P2,SIG,UTIL,UTILE,SOURE,XII,XI,JVX,IVX,KBVX,KVX,
     & MVX,JIVX,IDELAY)
      IF (IDELAY.EQ.0) RETURN
C
      REWIND IO26
      DO 100 K=1,KMAX
      READ(IO26) ((P2E(N1,KB,K),N1=1,JIVX),KB=1,KBVX)
  100 CONTINUE
      REWIND IO26
C     FORWARD FLUX IN CORE NOW
C
C     CALCULATE BIG DADDY
      BIGD = 0.0
      REWIND IO18
      DO 125 KB=1,KBMAX
      N1 = 0
      DO 124 I=1,IMAX
      DO 123 J=1,JMAX
      N1 = N1 + 1
      L  = NRGNE(J,I,KB)
      M = NCOMP(L)
      PV = PVOL(L)
      READ(IO18) (XTR1(K),K=1,KMAX)
C     IF(N1.EQ.1) WRITE(6,20) (XTR1(K),K=1,KMAX)
      DO 122 K=1,KMAX
      PA = XTR1(K)
      T1 = 0.0
      DO 121 KK=1,KMAX
      PF = P2E(N1,KB,KK)
      T1 = T1 + SIG(KK,M,4)*PV*PF
  121 CONTINUE
      BIGD = BIGD + XII(K,M)*PA*T1
  122 CONTINUE
  123 CONTINUE
  124 CONTINUE
  125 CONTINUE
      REWIND IO18
C
C
C     CALCULATE PROMPT NEUTRON LIFE TIME
      XLIFE = 0.0
      DO 129 KB=1,KBMAX
      N1 = 0
      DO 128 I=1,IMAX
      DO 127 J=1,JMAX
      N1 = N1 + 1
      L  = NRGNE(J,I,KB)
      M  = NCOMP(L)
      PV = PVOL(L)
      READ(IO18) (XTR1(K),K=1,KMAX)
      DO 126 K=1,KMAX
      PA = XTR1(K)
      PF = P2E(N1,KB,K)
      XLIFE = XLIFE + PV*SIG(K,M,5)*PA*PF
  126 CONTINUE
  127 CONTINUE
  128 CONTINUE
  129 CONTINUE
      XLIFE = XLIFE / BIGD * SQRT(.0253) / 2.2E+5
      WRITE(IOUT,1000) (TITL1(I),I=1,18)
      WRITE(IOUT,1001) (TITL2(I),I=1,18)
      XEF = 1.0 / SPARE(18)
      WRITE(IOUT,1009) XEF
      WRITE(IOUT,1003) BIGD
      WRITE(IOUT,1010) XLIFE
      XLIFE=XLIFE*SPARE(18)
      WRITE(IOUT,1011) XLIFE
C
C
C     EFFECTIVE DELAYED NEUTRON FRACTION
      REWIND IO18
CKSK  CALL DCLEA(BIEFF,IDELAY,0.0)
CKSK  CALL DCLEA(BLEFF,IDELAY,0.0)
CKSK  ERROR ON SUN(AP-3000) IF DCLEA IS USED, HP9000 IS OK
      DO 120 I=1,IDELAY
        BIEFF(I) = 0.0
        BLEFF(I) = 0.0
  120 CONTINUE
CKSK
C
      DO 135 KB=1,KBMAX
      N1 = 0
      DO 134 I=1,IMAX
      DO 133 J=1,JMAX
      N1 = N1 + 1
      L  = NRGNE(J,I,KB)
      M  = NCOMP(L)
      PV = PVOL(L)
      READ(IO18) (XTR1(K),K=1,KMAX)
      DO 132 II=1,IDELAY
      DO 131 K=1,KMAX
      SUM = 0.0
      SUML= 0.0
      DO 130 KK=1,KMAX
      SUM = SUM + BVSIGF(KK,II,M)*P2E(N1,KB,KK)
      SUML= SUML+ BLSIGF(KK,II,M)*P2E(N1,KB,KK)
  130 CONTINUE
      BIEFF(II)=BIEFF(II)+DXII(K,II,M)*XTR1(K)*SUM*PV
      BLEFF(II)=BLEFF(II)+DXII(K,II,M)*XTR1(K)*SUML*PV
  131 CONTINUE
  132 CONTINUE
  133 CONTINUE
  134 CONTINUE
  135 CONTINUE
      REWIND IO18
      BEFFF = 0.0
      ESTK = BIGD/SPARE(18)
      DO 136 II=1,6
      BIEFF(II)=BIEFF(II)/ESTK
      BEFFF=BEFFF+BIEFF(II)
      BLEFF(II)=1.00000/(BLEFF(II)/ESTK/BIEFF(II))
  136 CONTINUE
C
      IF(IDELAY.NE.15) GO TO 138
      BEFFF9 = 0.0
      DO 137 II=7,15
      BIEFF(II)=BIEFF(II)/ESTK
C     IF(BEFFF.EQ.1.0) BIEFF(II)=BIEFF(II)/0.8624
      BEFFF9= BEFFF9+BIEFF(II)
      BLEFF(II)=1.00000/(BLEFF(II)/ESTK/BIEFF(II))
  137 CONTINUE
      BEFF15= BEFFF +BEFFF9
C
  138 WRITE(IOUT,1004) BEFFF
      IF(IDELAY.EQ.15) WRITE(IOUT,1006) BEFFF9,BEFF15
      WRITE(IOUT,1005) (BIEFF(I),I=1,IDELAY)
      WRITE(IOUT,1007) (BLEFF(I),I=1,IDELAY)
C
      RETURN
 1000 FORMAT(1H1,18A4)
 1001 FORMAT(1H ,18A4)
 1003 FORMAT(1H0,'BIG DADDY IS',1PE14.6)
 1009 FORMAT(1H0,'K-EFFECTIVE IS',1PE14.6)
 1010 FORMAT(1H0,'PROMPT-NEUTRON LIFETIME IS',1PE13.5,'  SEC.')
 1011 FORMAT(1H ,'PROMPT-NEUTRON GENERATION TIME IS',1PE13.5,'  SEC.')
 1004 FORMAT(1H0,'DELAYED NEUTRON FRACTION IS',1PE13.5)
 1005 FORMAT(1H0,'EFFECTIVE DELAYED NEUTRON FRACTION FOR EACH GROUP'/
     &       (1H ,1P6E13.5))
 1006 FORMAT(1H ,'DELAYED PHOTO NEUTRON FRACTION IS',1PE13.5,
     &         /' TOTAL DELAYED NEUTRON FRACTION IS',1PE13.5)
 1007 FORMAT(1H0,'EFFECTIVE DELAYED NEUTRON DECAY CONSTANTS FOR ',
     & 'EACH GROUP'/ (1H ,1P6E13.5))
      END
