C **********************************************************************
                      SUBROUTINE ANISN1
C ******************* 1987/5/11 ****************************************
C   *** PLSNT CONTROLS LINK 1 WHICH DOES EVERYTHING PRIOR TO ITERATION
C        360 VERSION OF SUBROUTINE PLSNT REVISED  10-28-68
      COMMON /SKPBUF/ LOCATN,JUMP,LFAST,LSLOW,JMP1,JMP2,JMP3
      COMMON /SN1C/
     &              D(1),LIM1,LR,LW,LDSN,LMA,LMZ,LMB,LMC,LXMD,LFIX,LFLT,
     1       LJ5,LRM,LDF,LJ3,LJ4,LIGT,LART,LALFT,
     2LFGP,LFGG,LEND,LV,LAA,LWD,LMR,LPNC,
     BID,ITH,ISCT,ISN,IGE,IBL,IBR,IZM,IM,IEVT,IGM,IHT,IHS,IHM,MS,MCR,MTP
     C,MT,IDFM,IPVT,IQM,IPM,IPP,IIM,ID1,ID2,ID3,ID4,ICM,IDAT1,IDAT2,IFG,
     DIFLU,IFN,IPRT,IXTR,
     EEV,EVM,EPS,BF,DY,DZ,DFM1,XNF,PV,RYF,XLAL,XLAH,EQL,XNPM,
     FNMP,LCNMP,NXRA,LCMAC,LCVOLM,T(7),NIN,NOU,MM,JT
      COMMON /MAINC/ III(1000)
      DIMENSION IDIQ(1),TITLE(18),CASE(2)
      EQUIVALENCE (D(1),IDIQ(1)),(III(64),NOUT1),(III(65),NOUT2),
     1           (III(101),CASE(1)),(III(103),TITLE(1))
C     LIM1=1000 /* DEFINED FROM MAIN */
      CALL CLEAR(0.0,D(3),LIM1-2)
C *****
CDEL   NIN=5
       NIN=III(62)
       NOU=NOUT2
      NERR=0
C *****
      JUMP=0
      EV=0.0
      EVM=0.0
      XLA=0.0
C1000 FORMAT(12A4,12X,I12)
      WRITE(NOU,50) CASE,TITLE
   50 FORMAT(1H1,9X,'**** CASE ',2A4,' **** TITLE ',18A4,
     1      ' ** STEP - ANISN - ')
      WRITE(NOUT1,60)
   60 FORMAT(10X,'STEP - ANISN1 -')
      LFIX=29
      LFLT=65
C  *** READ PARAMETERS 15& AND 16*
      CALL SN1AR (  3,J3,NIN,NOU)
C  *** TRANSFER ISCT TO MAINC
      III(39)=ISCT
C
      IF(J3.NE.0)NERR=NERR+J3
    2 MP=1
      IMP=0
      MM=ISN+1
      IF(IGE.EQ.2)MM=(ISN*(ISN+4))/4
      IP=IM+1
      MI=MM*IP
      IZP=IZM+1
      JT=1
      IF(ISCT.GT.0)JT=ISCT
      IF(IGE.EQ.2)JT=(JT*(JT+4))/4
    3 IMJT=IM*JT
      IF(IPM.EQ.0)GO TO 5
      MP=MM
      IMP=IPM
C  *** COMPUTE BASE ADDRESSES
    5 LR=110
      LW=LR+IP
      LDSN=LW+MM
C     MAT ID BY INTERVAL
      LMA=LDSN+MM
C     MAT ID BY ZONE
      LMZ=LMA+IM
C     M-REG  BY ZONE
      LMB=LMZ+IZM
      LMC=LMB+IZM
      LXMD=LMC+MS
      LFLT=LXMD+MS
      LFIX=LFLT
C     SCATTERING ORDER BY ZONE
      LJ5=LFIX
      LRM=LJ5+IZM
      LDF=LRM+IZM
      LJ3=LDF+IM*IDFM
      LJ4=LJ3+ID3
C     X-REG NO. BY ZONE
      LFGP=LJ4+ID3
C     SINGED MAT ID  BY ZONE
      LFGG = LFGP+IZM
C     VOLUME BY INTERVAL
      LV = LFGG+IZM
C     AREA BY INTERVAL
      LAA=LV + IM
      LWD=LAA + IP
      LMR=LWD + MM
      LPNC=LMR + MM
      LCH=LPNC
      LEND=LPNC
      IF(ISCT.NE.0) LEND=LPNC+MM*JT
C  *** SN CONST DEFAULT SET
      CALL SNCONA(D(LDSN),D(LW),ISN,IGE)
C  *** READ REST OF DATA
      CALL SN1AR(  3,J3,NIN,NOU)
C     SEQUENTIAL MAT POSITION  BY ZONE
      LCMAC = LEND
C     MAT ID BY M-REG
      LCNMP = LCMAC + IZM
C
C REARANGEMENT MATERIAL NUMBER SPECIFICATION
C
      NMP   = 1
      IDIQ(LCMAC) = 1
      IDIQ(LMB) = 1
      IDIQ(LCNMP) = IDIQ(LMZ)
      IDIQ(LFGG)  = IDIQ(LMZ)
      IF (IZM.EQ.1) GO TO 15
      DO 13 I = 2,IZM
      MATNO = IDIQ(LMZ+I-1)
      IDIQ(LFGG+I-1)  = IDIQ(LMZ+I-1)
      I1    = I - 1
      DO 10 J = 1,I1
      IF(MATNO.EQ.IDIQ(LMZ+J-1)) GO TO 12
   10 CONTINUE
      NMP = NMP + 1
      IDIQ(LCNMP+NMP-1) = MATNO
      IDIQ(LCMAC+I-1)   = NMP
      IDIQ(LMB  +I-1)   = NMP
      GO TO 13
   12 CONTINUE
      IDIQ(LCMAC+I-1) = IDIQ(LCMAC+J-1)
      IDIQ(LMB  +I-1) = IDIQ(LMB  +J-1)
   13 CONTINUE
   15 CONTINUE
C
C ANISN MATERIAL NUMBER REARRANGE BY 19&
C
      IF (ISCT.EQ.0) GO TO 22
C    CHANGE SIGN OF MAT ID IF ANISOTROPIC
      DO 1001 I= 1,NMP
      MAX = 0
      DO 1000 J= 1,IZM
      IF(I.NE.IDIQ(LCMAC+J-1)) GO TO 1000
      MAX = MAX0(MAX,IDIQ(LJ5+J-1))
 1000 CONTINUE
      IF(MAX.NE.0) IDIQ(LCNMP+I-1) = -IDIQ(LCNMP+I-1)
 1001 CONTINUE
      DO 1002 J=1,IZM
      IF(IDIQ(LJ5+J-1).GT.0) IDIQ(LFGG+J-1)=-IDIQ(LFGG+J-1)
 1002 CONTINUE
C
C   INCREASE MAT SEQUENTIAL NO IF PREVIOUS IS ANISOTROPIC
C
      IF (NMP.EQ.1) GO TO 22
      LCMAC1 = LCNMP + NMP
      IMAT = 1
      DO 19 I =1,NMP
      DO 17 J =1,IZM
      IF (I.EQ.IDIQ(LCMAC+J-1)) IDIQ(LCMAC1+J-1) = IMAT
   17 CONTINUE
      MATNO = IDIQ(LCNMP+I-1)
CKSK  IF (MATNO.LT.0) IMAT = IMAT + 2
      IF (MATNO.LT.0) IMAT = IMAT + ISCT + 1
      IF (MATNO.GT.0) IMAT = IMAT + 1
   19 CONTINUE
      DO 24 I=1,IZM
      IDIQ(LCMAC+I-1) = IDIQ(LCMAC1+I-1)
   24 CONTINUE
   22 CONTINUE
C
C NUMBER OF X=REGION
      NXRA = 0
      DO 23 I =LFGP,LFGP+IZM-1
      NXRA = MAX0(NXRA,IDIQ(I))
   23 CONTINUE
      LCVOLM = LCNMP + NMP
      I01    = LCVOLM + NMP
      LEND=I01
      IF(I01.LE.LIM1)GO TO 6
C     IF(IDAT1.EQ.2)CALL ERRO(4HCORE,I01)
      WRITE(NOUT1,3000) I01,LIM1
      STOP
 3000 FORMAT('0***ERROR*** MEMORY OVERFLOW /SN1C/ USED =',I6,'+? WORDS',
     *       ' ALLOCATED =',I6,' WORDS'    )
    6 WRITE (NOU,20) I01
      WRITE(NOUT1,20) I01
   20 FORMAT(1H0,I6,40H LOCATIONS WILL BE USED FOR THIS PROBLEM)
      IF(J3.NE.0)NERR=J3 + NERR
C     IF(NERR.NE.0)CALL ERRO(  'DATA',NERR)
      IF(NERR.NE.0) THEN
              WRITE(NOUT1,3020)
              STOP
              ENDIF
 3020 FORMAT('0***ERROR*** ERROR STOPPED FOR PREVIOUS ERROR')
      I01=LEND
      I02=I01
      I03=I02
      I04=I03
      I05=I04
      I08=I05
      I09=I08
      IF(IGE.NE.2)GO TO 16
      I02=I01+MM
      I03=I02+MM
      I09=ISCT+1
      I08=I09+ISCT
      I04=I03 + MM
      I05=I04 + MM*I09*I09
      I06=I05 + I08
      CALL ADDR(123,127,DUM,DUM)
C     IF(I06.GT.LIM1)CALL ERRO(  'SNCR',I06)
      IF(I06.LE.LIM1) GO TO 16
      WRITE(NOUT1,3010) I06,LIM1
      STOP
 3010 FORMAT('0***ERROR*** MEMORY OVERFLOW /SN1C/ USED =',I6,'WORDS ',
     *       'ALLOCATED =',I6,' WORDS'   )
C   *** CHECK SN CONSTANTS, COMPUTE PL CONSTANTS
   16 CALL S804(D(LDSN),D(LW),D(LWD),D(LMR),D(LPNC),D(I01),D(I02),D(I03)
     1 ,D(I04),D(I05),MM,I09,I08)
CKSK 24 MARCH 1997 BY OKUMURA FOR PC-SRAC ---------------
      DO 25 JJJ=1, NMP
        D(LCVOLM+JJJ-1) = 0.0
   25 CONTINUE
CKSK ----------------------------------------------------
      CALL S814B(D(LV),D(LAA),IGE,IP,IM,D(LR),
     1          D(LCVOLM),D(LMA),D(LMB))
C    1          D(LCVOLM),D(LMA),D(LCMAC))
C     WRITE(NOU,30) (D(I),I=37,70)
CKSK  WRITE(NOU,30) (IDIQ(I),I=29,62)
      WRITE(NOU, 30) (IDIQ(I), I=29, 38)
      WRITE(NOU, 32) (IDIQ(I), I=39, 48)
      WRITE(NOU, 34) (IDIQ(I), I=49, 62)
C   *** NOTE UTILIZATION-19 CONTINUE CARDS IN WORLDS MOST ELABORATE
C       FORMAT
   30 FORMAT('0 ID    PROBLEM ID NO.',                   8X,I13,7X,
     #         'ITH   0/1 = REG./ADJ.',                  9X,I13,/,
     #       '  ISCT  ORDER OF SCATTERING',              3X,I13,7X,
     #         'ISN   QUADRATURE ORDER',                 8X,I13,/,
     #       '  IGE   1/2/3 = PLA/CYL/SPH',              3X,I13,7X,
     #         'IBL   0/1/2/3 = NO REFL/REFL/PER/WHITE', I5,/,
     #       '  IBR   RT. B.C. SAME AS LEFT B.C.,IBL',   I5,7X,
     #         'IZM   NO. OF ZONES',                     12X,I13,/,
     #       '  IM    No. OF INTERVALS',                 6X,I13,7X,
     #         'IEVT  0/1/2/3/4/5/6=Q/K/ALPHA/C/Z/R/H',  I6)

   32 FORMAT('  IGM   NO. OF GROUPS',                    9X,I13,7X,
     #         'IHT   POS. OF SIGMA T',                  9X,I13,/,
     #       '  IHS   POS. OF SIGMA GG',                 6X,I13,7X,
     #         'IHM   TABLE LENGTH',                     12X,I13,/,
     #       '  MS    MIXING TABLE LENGTH',              3X,I13,7X,
     #         'MCR   NO. MATLS. FROM CARDS',            3X,I13,/,
     #       '  MTP   NO. MATLS. FROM LIB TAPE',         I11,7X,
     #         'MT    NO. OF MATLS',                     12X,I13,/,
     #       '  IDFM  0/1=NONE/DENSITY FACTORS(21*)',    I6,7X,
     #         'IPVT  0/1/2=NONE/K/ALPHA',               6X,I13)

   34 FORMAT('  IQM   0/1=NONE/DIST. SOURCE ',           I13,7X,
     #         'IPM   0/1/IM=NONE/S(MM,IPP)/S(MM,IM)',   I7,/,
     #       '  IPP   INTERVAL OF SHELL SOURCE',         I11,7X,
     #         'IIM   INNER ITER. MAX.',                 8X,I13,7X,/,
     #       '  ID1   0/1/2/3=NO/PRNT ND/PNCH N/BOTH',   I5,7X,
     #         'ID2   0/1/2=NO/X-SEC TAPE/PREV',         I13,/,
     #       '  ID3   0/N=NO/N ACT. BY ZONE',            1x,I13,7X,
     #         'ID4   0/1=NO/N ACT. BY INT.',            3x,I13,/,
     #       '  ICM   OUTER ITER. MAX.',                 6X,I13,7X,
     #         'IDAT1 0/1/2=NO/MIN/MAX TAPE',            I16,/,
     #       '  IDAT2 0/1=NO/DIFFUSION(24$)',            1x,I13,7X,
     #         'IFG   0/1=NO/FEW GRP.',                  9X,I13,/,
     #       '  IFLU  0/1/2=BOTH/LINEAR/STEP',           6x,I7,7X,
     #         'IFN   0/1/2=INPUT 2*/3*/PREV. CASE',     2x,I7)
CKSK
C   *** NOTE UTILIZATION-19 CONTINUE CARDS IN WORLDS MOST ELABORATE
C       FORMAT
*  30 FORMAT(22H0 ID    PROBLEM ID NO.8X,I13,7X,21HITH   0/1 = REG./ADJ.
*    19X,I13/27H  ISCT  ORDER OF SCATTERING3X,I13,7X22HISN   QUADRATURE
*    2ORDER8X,I13/27H  IGE   1/2/3 = PLA/CYL/SPH3X,I13,7X38HIBL   0/1/2/
*    33 = NO REFL/REFL/PER/WHITEI5/38H  IBR   RT. B.C. SAME AS LEFT B.C.
*    4,IBLI5,7X,18HIZM   NO. OF ZONES12X,I13/24H  IM    NO. OF INTERVALS
*    56X,I13,7X,37HIEVT  0/1/2/3/4/5/6=Q/K/ALPHA/C/Z/R/HI6/21H  IGM   NO
*    6. OF GROUPS9X,I13,7X,21HIHT   POS. OF SIGMA T9X,I13/24H  IHS   POS
*    7. OF SIGMA GG6X,I13,7X,18HIHM   TABLE LENGTH12X,I13/27H  MS    MIX
*    8ING TABLE LENGTH3X,I13,7X,27HMCR   NO. MATLS. FROM CARDS3X,I13/32H
*    9  MTP   NO. MATLS. FROM LIB TAPEI11,7X,19HMT    NO. OF MATLS,11X,I
*    A13/37H  IDFM  0/1=NONE/DENSITY FACTORS(21*)I6,7X,24HIPVT  0/1/2=NO
*    BNE/K/ALPHA6X,I13/30H  IQM   0/1=NONE/DIST. SOURCE I13,7X,36HIPM
*    C0/1/IM=NONE/S(MM,IPP)/S(MM,IM)I7/32H  IPP   INTERVAL OF SHELL SOUR
*    DCEI11,7X,22HIIM   INNER ITER. MAX.8X,I13,7X/38H  ID1   0/1/2/3=NO/
*    EPRNT ND/PNCH N/BOTHI5,7X,30HID2   0/1/2=NO/X-SEC TAPE/PREVI13/30H
*    F ID3   0/N=NO/N ACT. BY ZONE I13,7X,30HID4   0/1=NO/N ACT. BY INT.
*    G   I13/24H  ICM   OUTER ITER. MAX.6X,I13,7X,27HIDAT1 0/1/2=NO/MIN/
*    HMAX TAPE  I16/30H  IDAT2 0/1=NO/DIFFUSION(24&) I13,7X21HIFG   0/1=
*    INO/FEW GRP.9X,I13/36H  IFLU  0/1/2=BOTH/LINEAR/STEP      I7,7X,36H
*    JIFN   0/1/2=INPUT 2*/3*/PREV. CASE  I7)
CKSK
C     WRITE(NOU,40) (D(I),I=71,86)
      WRITE(NOU,40)  IDIQ(63),IDIQ(64),(D(I),I=65,78)
CKSK
   40 FORMAT('  IPRT  0/1= PRINT X-SEC/DO NOT',         6x,I6,7X,
     #         'IXTR  0/1=CALC/READ P-L CONSTANTS',     4x,I6,/,
     #       '0 EV    EIGENVALUE GUESS',                6X,1PE13.5,7X,
     #         'EVM   EIGENVALUE MODIFIER',             5X,E13.5,/,
     #       '  EPS   PRECISION DESIRED',               5X,E13.5,7X,
     #         'BF    BUCKLING FACTOR',                 9X,E13.5,/,
     #       '  DY    CYL OR PLA HEIGHT',               5X,E13.5,7X,
     #         'DZ    PLANE DEPTH',                     13X,E13.5,/,
     #       '  DFM1  HT. FOR VOID CORR.',              4X,E13.5,7X,
     #         'XNF   NORM. FACTOR',                    12X,E13.5,/,
     #       '  PV    IPVT=1/2 - K/ALPHA',              4X,E13.5,7X,
     #         'RYF   LAMBDA2 RELAXATION',              6X,E13.5,/,
     #       '  XLAL  PT CNVRG EPS. IF .NE.0',          E13.5,7X,
     #         'XLAH  1-LAMBDA MAX.-SEARCH',            4X,E13.5,/,
     #       '  EQL   EV CHANGE EPS.-SEARCH',           1x,E13.5,7X,
     #         'XNPM  NEW PARAM. MOD.-SEARCH',          2x,E13.5) 
CKSK
*  40 FORMAT                                            (37H  IPRT  0/1
*    A= PRINT X-SEC/DO NOT     I6,7X37HIXTR  0/1=CALC/READ P-L CONSTANTS
*    B    I6/24H0 EV    EIGENVALUE GUESS6X,1PE13.5,7X,25HEVM   EIGENVALU
*    1E MODIFIER5X,E13.5/25H  EPS   PRECISION DESIRED5X,E13.5,7X,21HBF
*    2  BUCKLING FACTOR9X,E13.5/25H  DY    CYL OR PLA HEIGHT5X,E13.5,7X,
*    317HDZ    PLANE DEPTH13X,E13.5/26H  DFM1  HT. FOR VOID CORR.4X,E13.
*    45,7X,18HXNF   NORM. FACTOR12X,E13.5/26H  PV    IPVT=1/2 - K/ALPHA4
*    5X,E13.5,7X,24HRYF   LAMBDA2 RELAXATION6X,E13.5/30H  XLAL  PT CNVRG
*    6 EPS. IF .NE.0E13.5,7X,26HXLAH  1-LAMBDA MAX.-SEARCH4X,E13.5/30H
*    7EQL   EV CHANGE EPS.-SEARCH E13.5,7X,30HXNPM  NEW PARAM. MOD.-SEAR
*    8CH  E13.5)
CKSK
C   *** PRINT INPUT DATA
      DO 21 I=1,IZM
      I01=LJ5+I-1
      I02=I01+IZM
      IF(ISCT.EQ.0)D(I01)=0.0
   21 IF(IEVT.NE.4)D(I02)=0.0
C     WRITE (NOU,50) T
      WRITE (NOU,100)
  100 FORMAT(71H0 INT.  ZONE NUMBER     RADIUS        AREA        VOLUME
     1    DENS FACTOR )
      CALL WOT8(6,IM,3,IP,25,IP,24,IM,15,IM*IDFM,0,0,0,0,0,0,NOU)
C     WRITE (NOU,50) T
      WRITE (NOU,110)
  110 FORMAT(97H0        RT ALBEDO    LFT ALBEDO  DIFF MARKER   MAT'L/ZO
     1NE   L OF P(L)   RADIUS MOD  X-REG'N/ZONE  )
      CALL WOT8(19,IGM,20,IGM,18,IGM,7 ,IZM,13,IZM,14,IZM,21,IZM,0,0,NOU
     *                              )
C     WRITE (NOU,50) T
      WRITE (NOU,120)
CKSK
  120 FORMAT(1H0,13X,'CROSS SECTION MIXING TABLE',29X,
     #       'ANGULAR  QUADRATURE  CONSTANTS',/,9X,
     #       'MIXTURE     COMPONENT    NO. DENSITY',15X,
     #       'COSINE (MU)     WEIGHT    REFL DIRECT   WT. X COS.')  
* 120 FORMAT(1H013X26HCROSS SECTION MIXING TABLE29X30HANGULAR  QUADRATUR
*    1E  CONSTANTS/9X36HMIXTURE     COMPONENT    NO. DENSITY15X50HCOSINE
*    2 (MU)     WEIGHT    REFL DIRECT   WT. X COS.)
CKSK
      CALL WOT8( 8,MS, 9,MS,10,MS,0,0,5,MM,4,MM,27,MM,26,MM,NOU)
      IF(ISCT.LE.1)GO TO 999
      WRITE (NOU,130) ISCT
  130 FORMAT(18H0 CONSTANTS FOR P(,I2,12H) SCATTERING)
      CALL WOT(D(LPNC),JT,MM,1,'ANGL',' SET',0)
  999 WRITE(NOU,616) 'NUMBER OF MATERIALS         =', NMP
      WRITE(NOU,616) 'MAT.ID BY ZONE (INPUT)      =',
     @ (IDIQ(I),I=LMZ,LMZ+IZM-1)
      WRITE(NOU,616) 'M REG  BY ZONE              =',
     @ (IDIQ(I),I=LMB,LMB+IZM-1)
      WRITE(NOU,616) 'SIGNED MAT.ID BY ZONE       =',
     @ (IDIQ(I),I=LFGG,LFGG+IZM-1)
      WRITE(NOU,616) 'SEQUENTIAL MAT.POS. BY ZONE =',
     @ (IDIQ(I),I=LCMAC,LCMAC+IZM-1)
      WRITE(NOU,616) 'SIGNED MAT.ID BY MATERIAL   =',
     @ (IDIQ(I),I=LCNMP,LCNMP+NMP-1)
      WRITE(NOU,616) 'NUMBER OF X-REGIONS         =', NXRA
  616 FORMAT(1X,A,20I4)
      WRITE(NOU,617) 'VOLUME  MATERIAL   =',
     @ (D(I),I=LCVOLM,LCVOLM+NMP-1)
  617 FORMAT(1X,A,8E13.5/20X,8E13.5)
      RETURN
      END
